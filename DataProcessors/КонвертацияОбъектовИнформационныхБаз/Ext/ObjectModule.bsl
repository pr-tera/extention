#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПростыеРешения

&ИзменениеИКонтроль("ПроизвестиЧтениеДанных")
Процедура пр_ПроизвестиЧтениеДанных(ЧтениеСообщения)

	СтрокаСообщенияОбОшибке = "";

	Пока ФайлОбмена.Прочитать() Цикл

		ИмяУзла = ФайлОбмена.ЛокальноеИмя;

		Если ИмяУзла = "Объект" Тогда

			ОбменДаннымиСервер.РассчитатьПроцентЗагрузки(СчетчикЗагруженныхОбъектов(), КоличествоОбъектовКЗагрузке, РазмерФайлаСообщенияОбмена);
			ПоследнийОбъектЗагрузки = ПрочитатьОбъект();

			ОбработатьОкончаниеЧтенияНовогоЭлемента(ПоследнийОбъектЗагрузки);

		ИначеЕсли ИмяУзла = "НаборЗаписейРегистра" Тогда

			// набор записей регистра
			ПоследнийОбъектЗагрузки = ПрочитатьНаборЗаписейРегистра();

			ОбработатьОкончаниеЧтенияНовогоЭлемента(ПоследнийОбъектЗагрузки);

		ИначеЕсли ИмяУзла = "УдалениеОбъекта" Тогда

			// Обработка удаления объекта из информационной базы.
			ПрочитатьУдалениеОбъекта(СтрокаСообщенияОбОшибке);

			одПропустить(ФайлОбмена, "УдалениеОбъекта");

			ОбработатьОкончаниеЧтенияНовогоЭлемента();

		ИначеЕсли ИмяУзла = "ИнформацияОРегистрацииОбъекта" Тогда

			ЕстьИнформацияОРегистрацииОбъекта = Истина;

			ПоследнийОбъектЗагрузки = ПрочитатьИнформациюОРегистрацииОбъекта();

			ОбработатьОкончаниеЧтенияНовогоЭлемента(ПоследнийОбъектЗагрузки);

		ИначеЕсли ИмяУзла = "КорректировкаИнформацииОРегистрацииОбъекта" Тогда

			ЕстьКорректировкаИнформацииОРегистрацииОбъекта = Истина;

			ПрочитатьКорректировкуИнформацииСопоставления();

			одПропустить(ФайлОбмена, ИмяУзла);

		ИначеЕсли ИмяУзла = "ОбщиеДанныеУзлов" Тогда

			ПрочитатьОбщиеДанныеУзлов(ЧтениеСообщения);

			одПропустить(ФайлОбмена, ИмяУзла);

		ИначеЕсли (ИмяУзла = "ФайлОбмена") И (ФайлОбмена.ТипУзла = ТипУзлаXMLКонецЭлемента) Тогда

			Прервать; // выходим
		#Вставка 
		// обработка лишнего узла в файле обмена
		ИначеЕсли ИмяУзла = "ДанныеПоФоновомуОбмену"  Тогда
			Продолжить;
		
		#КонецВставки
		
		Иначе

			ВызватьИсключение НСтр("ru = 'Ошибка формата сообщения обмена.'");

		КонецЕсли;

		// Прерываем цикл чтения файла в случае возникновения ошибки загрузки.
		Если ФлагОшибки() Тогда
			ВызватьИсключение НСтр("ru = 'Возникли ошибки при загрузке данных.'");
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&Вместо("ИмяПланаОбмена")
Функция пр_ИмяПланаОбмена()

	Результат = ПродолжитьВызов();
	
	Если Результат = "ОбменУППУПП" Тогда
		
		Результат = "ОбменУправлениеНебольшойФирмойБухгалтерия20";
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&Вместо("ПрочитатьУдалениеОбъекта")
Процедура пр_ПрочитатьУдалениеОбъекта(СтрокаСообщенияОбОшибке)
	
	ТипИсточникаСтрокой = одАтрибут(ФайлОбмена, ТипСтрока, "ТипПриемника");
	
	Если ЗначениеЗаполнено(ТипИсточникаСтрокой) Тогда
		ПродолжитьВызов(СтрокаСообщенияОбОшибке);
	Иначе
		пр_ПрочитатьУдалениеОбъектаВместо(СтрокаСообщенияОбОшибке);	
	КонецЕсли; 

КонецПроцедуры

Процедура пр_ПрочитатьУдалениеОбъектаВместо(СтрокаСообщенияОбОшибке)
	
	УникальныйИдентификаторСтрокой = одАтрибут(ФайлОбмена, ТипСтрока, "СсылкаНаОбъект");
	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(УникальныйИдентификаторСтрокой, ",", Ложь);
	Если МассивСтрок.Количество() > 1 Тогда
		УникальныйИдентификаторСтрокой = МассивСтрок[1];
	КонецЕсли; 
	
	Для каждого Менеджер Из Менеджеры Цикл
		
		СтруктураСвойств = Менеджер;
		
		УникальныйИдентификаторСсылки = Новый УникальныйИдентификатор(УникальныйИдентификаторСтрокой);
		
		Попытка
			Ссылка = СтруктураСвойств.Значение.Менеджер.ПолучитьСсылку(УникальныйИдентификаторСсылки);
		Исключение
		    Ссылка = Неопределено;
		КонецПопытки; 
		
		Если НЕ Ссылка = Неопределено И Ссылка.ПолучитьОбъект() <> Неопределено  Тогда
			УдалитьОбъектПоСсылке(Ссылка, СтрокаСообщенияОбОшибке);
			Возврат;
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

&ИзменениеИКонтроль("ПрочитатьДанныеПоОбмену")
Процедура пр_ПрочитатьДанныеПоОбмену(ЧтениеСообщения, АнализДанных)

	ПолеИмяПланаОбмена           = одАтрибут(ФайлОбмена, ТипСтрока, "ПланОбмена");
	КодОтКого                    = одАтрибут(ФайлОбмена, ТипСтрока, "ОтКого");
	ПолеНомерСообщения           = одАтрибут(ФайлОбмена, ТипЧисло,  "НомерИсходящегоСообщения");
	ПолеНомерПринятогоСообщения  = одАтрибут(ФайлОбмена, ТипЧисло,  "НомерВходящегоСообщения");
	УдалитьРегистрациюИзменений  = одАтрибут(ФайлОбмена, ТипБулево, "УдалитьРегистрациюИзменений");
	ВерсияОтправителя            = одАтрибут(ФайлОбмена, ТипСтрока, "ВерсияОтправителя");
	
	#Вставка 
	
	Если КодОтКого = "УП" Тогда
		УдалитьРегистрациюИзменений = Истина;	
	КонецЕсли;
		
	#КонецВставки
	УзелОбменаПолучатель = ПланыОбмена[ИмяПланаОбмена()].НайтиПоКоду(КодОтКого);

	// Проверка на наличие узла получателя
	// проверка на правильность указания узла получателя в сообщении обмена.
	Если Не ЗначениеЗаполнено(УзелОбменаПолучатель)
		ИЛИ УзелОбменаПолучатель <> УзелОбменаЗагрузкаДанных Тогда

		СтрокаСообщения = НСтр("ru = 'Не найден узел обмена для загрузки данных. План обмена: %1, код: %2.'");
		СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаСообщения, ИмяПланаОбмена(), КодОтКого);
		ВызватьИсключение СтрокаСообщения;
	КонецЕсли;

	ЧтениеСообщения = ОписаниеЧтенияСообщения();
	ЧтениеСообщения.Отправитель       = УзелОбменаПолучатель;
	ЧтениеСообщения.ОтправительОбъект = УзелОбменаПолучатель.ПолучитьОбъект();
	ЧтениеСообщения.НомерСообщения    = ПолеНомерСообщения;
	ЧтениеСообщения.НомерПринятого    = ПолеНомерПринятогоСообщения;
	ЧтениеСообщения.СообщениеБылоПринятоРанее = Ложь;
	ЧтениеСообщения.АнализДанных = АнализДанных;
	ЧтениеСообщения.Вставить("ВосстановленаРезервнаяКопия",
	ЧтениеСообщения.НомерПринятого > ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЧтениеСообщения.Отправитель, "НомерОтправленного"));

	ЧтениеСообщения = Новый ФиксированнаяСтруктура(ЧтениеСообщения);

	Если РежимЗагрузкиДанныхВИнформационнуюБазу() Тогда

		НачатьТранзакцию();
		Попытка
			НомерПринятого = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЧтениеСообщения.Отправитель, "НомерПринятого");
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
		КонецПопытки;

		Если НомерПринятого >= ЧтениеСообщения.НомерСообщения Тогда // Номер сообщения меньше либо равен ранее принятому.

			ЧтениеСообщенияВременное = ОбщегоНазначения.СкопироватьРекурсивно(ЧтениеСообщения, Ложь);
			ЧтениеСообщенияВременное.СообщениеБылоПринятоРанее = Истина;
			ЧтениеСообщения = Новый ФиксированнаяСтруктура(ЧтениеСообщенияВременное);

			ВызватьИсключение НСтр("ru = 'Сообщение обмена было принято ранее'");
		КонецЕсли;

		УдалитьРегистрациюИзменений = УдалитьРегистрациюИзменений И Не ЧтениеСообщения.ВосстановленаРезервнаяКопия;

		Если УдалитьРегистрациюИзменений Тогда // Удаляем регистрацию изменений.

			Если ТранзакцияАктивна() Тогда
				ВызватьИсключение НСтр("ru = 'Удаление регистрации изменений данных не может быть выполнено в активной транзакции.'");
			КонецЕсли;

			ПланыОбмена.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);

			РегистрыСведений.ИзмененияОбщихДанныхУзлов.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);

			Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияФорматаВходящегоСообщенияОбмена(), "3.1.0.0") >= 0 Тогда

				РегистрыСведений.ОбщиеНастройкиУзловИнформационныхБаз.ЗафиксироватьВыполнениеКорректировкиИнформацииСопоставления(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);

			КонецЕсли;

			РегистрыСведений.ОбщиеНастройкиУзловИнформационныхБаз.СнятьПризнакНачальнойВыгрузкиДанных(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);

		КонецЕсли;

		Если ЧтениеСообщения.ВосстановленаРезервнаяКопия Тогда
			ОбменДаннымиСервер.ПриВосстановленииРезервнойКопии(ЧтениеСообщения);

			ЧтениеСообщенияВременное = ОбщегоНазначения.СкопироватьРекурсивно(ЧтениеСообщения, Ложь);
			ЧтениеСообщенияВременное.ОтправительОбъект = ЧтениеСообщения.Отправитель.ПолучитьОбъект();
			ЧтениеСообщения = Новый ФиксированнаяСтруктура(ЧтениеСообщенияВременное);
		КонецЕсли;

		РегистрыСведений.ОбщиеНастройкиУзловИнформационныхБаз.УстановитьВерсиюКорреспондента(ЧтениеСообщения.Отправитель, ВерсияОтправителя);

	КонецЕсли;

	// {Обработчик: ПослеПолученияИнформацииОбУзлахОбмена} Начало
	Если Не ПустаяСтрока(Конвертация.ПослеПолученияИнформацииОбУзлахОбмена) Тогда

		Попытка

			Если ОтладкаОбработчиковЗагрузки Тогда

				ВыполнитьОбработчик_Конвертация_ПослеПолученияИнформацииОбУзлахОбмена(ЧтениеСообщения.Отправитель);

			Иначе

				Выполнить(Конвертация.ПослеПолученияИнформацииОбУзлахОбмена);

			КонецЕсли;

		Исключение
			ВызватьИсключение ЗаписатьИнформациюОбОшибкеОбработчикиКонвертации(176, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), НСтр("ru = 'ПослеПолученияИнформацииОбУзлахОбмена (конвертация)'"));
		КонецПопытки;

	КонецЕсли;
	// {Обработчик: ПослеПолученияИнформацииОбУзлахОбмена} Окончание

КонецПроцедуры

#КонецОбласти 	

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли