#Область ОбработчикиСобытийФормы

&НаСервере
Процедура пр_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	пр_ИзменениеЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура пр_пр_ПоказатьВсеКЛПосле(Команда)
	
	пр_ОтображатьВсеКИ = Не пр_ОтображатьВсеКИ;
	пр_ОбновитьЭлементыКонтактныхЛиц();
	
	Если пр_ОтображатьВсеКИ Тогда
		Элементы.пр_ПоказатьВсеКЛ.Заголовок = "свернуть";
	Иначе
		Элементы.пр_ПоказатьВсеКЛ.Заголовок = "показать все";
	КонецЕсли; 

	ТекущийЭлемент = Элементы.НаименованиеПолное;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура пр_ИзменениеЭлементовФормы()
	
	// Контаткные лица
	Элементы.ДополнительныеРеквизиты.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	Элементы.ДополнительныеРеквизиты.ОтображатьЗаголовок = Истина;

	// Дополнительное описание
	СвойстваГруппы = Новый Структура;
	СвойстваГруппы.Вставить("Вид", ВидГруппыФормы.ОбычнаяГруппа);
	СвойстваГруппы.Вставить("Заголовок", "Дополнительное описание");
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.КолонкаСправа, Элементы.Детали, СвойстваГруппы);	
	ГруппаДополнительноеОписание = РедакторФорм.ДобавитьГруппуНаФорму(КонтекстГруппы, "пр_ГруппаДополнительноеОписание");
	
	КонтекстПоля = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, ГруппаДополнительноеОписание);	
	КонтекстПоля.Свойства.Вставить("ПутьКДанным", 	"Объект.Комментарий");
	КонтекстПоля.Свойства.Вставить("Заголовок", 	"Дополнительное описание");
	КонтекстПоля.Свойства.Вставить("Доступность", 	Истина);
	КонтекстПоля.Свойства.Вставить("Высота", 		12);
	КонтекстПоля.Свойства.Вставить("Ширина", 		50);
	
	КонтекстПоля.Свойства.Вставить("АвтоМаксимальнаяШирина", 	Ложь);
	КонтекстПоля.Свойства.Вставить("РастягиватьПоГоризонтали", 	Ложь);
	КонтекстПоля.Свойства.Вставить("ПоложениеЗаголовка", 		ПоложениеЗаголовкаЭлементаФормы.Нет);
	
	ПолеФормы = РедакторФорм.ДобавитьПолеНаФорму(КонтекстПоля, "пр_ПовторноеДополнительноеОписание");
	
	Элементы.Комментарий.Видимость = Ложь;
	
	// Табличная часть пр_МенеджерыПокупателя
	СвойстваГруппы = Новый Структура;
	СвойстваГруппы.Вставить("Вид", 						ВидГруппыФормы.ОбычнаяГруппа);
	СвойстваГруппы.Вставить("Поведение", 				ПоведениеОбычнойГруппы.Свертываемая);
	СвойстваГруппы.Вставить("Заголовок", 				"Менеджеры покупателя");
	СвойстваГруппы.Вставить("Свернута", 				Истина);
	СвойстваГруппы.Вставить("ОтображениеУправления",	ОтображениеУправленияОбычнойГруппы.Картинка);
	
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.КолонкаСлева, Элементы.Взаиморасчеты, СвойстваГруппы);	
	ГруппаМенеджерыПокупателя = РедакторФорм.ДобавитьГруппуНаФорму(КонтекстГруппы, "пр_ГруппаМенеджерыПокупателя");
	
	СтруктураКолонок = Новый Структура;
	СтруктураКолонок.Вставить("Менеджер", 	"МенеджерПокупателя");
	СтруктураКолонок.Вставить("Дата", 		"CRM_Дата");
	
	СвойстваТаблицы = Новый Структура;
	СвойстваТаблицы.Вставить("АвтоМаксимальнаяШирина", Ложь);
	СвойстваТаблицы.Вставить("АвтоМаксимальнаяВысота", Ложь);
	СвойстваТаблицы.Вставить("МаксимальнаяШирина", 		50);
	СвойстваТаблицы.Вставить("Ширина", 					50);
	СвойстваТаблицы.Вставить("АвтоМаксимальнаяВысота", 	5);
	СвойстваТаблицы.Вставить("Высота", 					4);
	
	КонтекстТаблицы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, ГруппаМенеджерыПокупателя, ,);	
	ТаблицаФормы = РедакторФорм.ДобавитьТаблицуНаФорму(КонтекстТаблицы, "пр_МенеджерыПокупателя", "Объект.пр_МенеджерыПокупателя", СтруктураКолонок); 
	
	ТаблицаФормы.АвтоМаксимальнаяШирина = Ложь;
	ТаблицаФормы.АвтоМаксимальнаяВысота = Ложь;
	ТаблицаФормы.МаксимальнаяШирина 	= 50;
	ТаблицаФормы.Ширина 				= 50;
	ТаблицаФормы.АвтоМаксимальнаяВысота = 5;
	ТаблицаФормы.Высота 				= 4;
	
КонецПроцедуры
 
#Область КонтактныеЛица

&НаСервере
&Вместо("ОбновитьЭлементыКонтактныхЛиц")
Процедура пр_ОбновитьЭлементыКонтактныхЛиц()
	
	Элементы.Переместить(Элементы.ДобавитьПоляКонтактногоЛица, Элементы.КомандыДобавленияКонтакт_0);
	Элементы.КомандыДобавленияРастяжение_0.МаксимальнаяШирина = 41;
	
	УдаляемыеЭлементы 	= Новый Массив;
	УдаляемыеКоманды 	= Новый Массив;
	// Группа первого контактного лица создана в конфигураторе
	Для ИндексГруппы = 1 По Элементы.ВсеКонтакты.ПодчиненныеЭлементы.Количество()-1 Цикл
		УдаляемыеЭлементы.Добавить(Элементы.ВсеКонтакты.ПодчиненныеЭлементы[ИндексГруппы]);
		УдаляемыеКоманды.Добавить(Команды.Найти("КомандаУстановитьСвязь_"+ИндексГруппы));
		УдаляемыеКоманды.Добавить(Команды.Найти("КомандаРазорватьСвязь_"+ИндексГруппы));
	КонецЦикла;
	Для Каждого ГруппаКИ Из Элементы.КонтактнаяИнформацияКонтакт_0.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(ГруппаКИ);
	КонецЦикла;
	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	Для Каждого УдаляемаяКоманда Из УдаляемыеКоманды Цикл
		Команды.Удалить(УдаляемаяКоманда);
	КонецЦикла;
	
	ШиринаВидаКИ = 8;
	ШиринаПоляКомментария = 11;
	
	// {[+](фрагмент ДОБАВЛЕН), Андрей К. 25.07.2020 7:43:22
	пр_ЦветФонаГруппаКонтактногоЛица 			= Новый Цвет(250, 250, 250);
	пр_ЦветФонаГруппаОСновногоКонтактногоЛица 	= Новый Цвет(250, 250, 229);
	
	пр_МаксимальнаяШиринаПредстваленияКИ 	= 22;
	пр_МаксимальнаяШиринаГруппыКИ 			= 53;
	
	// Сортировка КИ
	ДанныеКонтактныхЛиц.Сортировать("Должность Убыв");
	
	// Основное контактное лицо всегда поставим первым
	ПараметрыОтбора = Новый Структура("КонтактноеЛицо", Объект.КонтактноеЛицо);
	СтрокиКЛ = ДанныеКонтактныхЛиц.НайтиСтроки(ПараметрыОтбора);
	
	Если СтрокиКЛ.Количество() > 0 Тогда
		
		НомерСтроки = 0;
		СтрокаОсновоногоКИ = СтрокиКЛ[0]; 

		Для каждого СтрокаКЛ Из ДанныеКонтактныхЛиц Цикл
			
			Если СтрокаКЛ = СтрокаОсновоногоКИ Тогда
				Прервать;	
			КонецЕсли; 
			
			НомерСтроки = НомерСтроки + 1;
			
		КонецЦикла;
		
		ДанныеКонтактныхЛиц.Сдвинуть(НомерСтроки, -1 * НомерСтроки); 
		
	КонецЕсли; 
	
	НомерСтроки = 0;
	// }Андрей К. 25.07.2020 7:43:22 

	Для Каждого ДанныеКонтактногоЛица Из ДанныеКонтактныхЛиц Цикл
		
		// {[+](фрагмент ДОБАВЛЕН), Андрей К. 08.09.2020 15:05:51
		НомерСтроки = НомерСтроки + 1;
		// }Андрей К. 08.09.2020 15:05:51 
		
		ИндексКонтакта = ДанныеКонтактныхЛиц.Индекс(ДанныеКонтактногоЛица);
		
		пр_ЛогикаУпрощенногоВыводаЗадействована = ЗначениеЗаполнено(ДанныеКонтактногоЛица.КонтактноеЛицо);
		
		// Для первого контактного лица элементы формы созданы в конфигураторе
		Если ИндексКонтакта > 0 Тогда
			
			ГруппаКонтактногоЛица = Элементы.Добавить("Контакт_" + ИндексКонтакта, Тип("ГруппаФормы"), Элементы.ВсеКонтакты);
			ГруппаКонтактногоЛица.Вид = Элементы.Контакт_0.Вид;
			ГруппаКонтактногоЛица.Отображение = Элементы.Контакт_0.Отображение;
			ГруппаКонтактногоЛица.Группировка = Элементы.Контакт_0.Группировка;
			ГруппаКонтактногоЛица.ОтображатьЗаголовок = Элементы.Контакт_0.ОтображатьЗаголовок;
			
			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 25.07.2020 7:38:35
			Если пр_ЛогикаУпрощенногоВыводаЗадействована Тогда
				
				Если НЕ пр_ОтображатьВсеКИ И НомерСтроки > 3 Тогда
					ГруппаКонтактногоЛица.Видимость = Ложь;		
				КонецЕсли; 
				
				ГруппаКонтактногоЛица.ЦветФона 					= пр_ЦветФонаГруппаКонтактногоЛица;	
				ГруппаКонтактногоЛица.РастягиватьПоГоризонтали 	= Ложь;
				ГруппаКонтактногоЛица.Ширина					= пр_МаксимальнаяШиринаГруппыКИ;
				
			КонецЕсли; 
			// }Андрей К. 25.07.2020 7:38:35 	
			
			ГруппаНаименованиеКонтактногоЛица = Элементы.Добавить("ГруппаНаименованиеКонтакт_" + ИндексКонтакта, Тип("ГруппаФормы"), ГруппаКонтактногоЛица);
			ГруппаНаименованиеКонтактногоЛица.Вид = Элементы.ГруппаНаименованиеКонтакт_0.Вид;
			ГруппаНаименованиеКонтактногоЛица.Отображение = Элементы.ГруппаНаименованиеКонтакт_0.Отображение;
			ГруппаНаименованиеКонтактногоЛица.Группировка = Элементы.ГруппаНаименованиеКонтакт_0.Группировка;
			
			ГруппаНаименованиеКонтактногоЛица.ОтображатьЗаголовок = Элементы.ГруппаНаименованиеКонтакт_0.ОтображатьЗаголовок;
			
			ПолеНаименование = Элементы.Добавить("НаименованиеКонтакт_" + ИндексКонтакта, Тип("ПолеФормы"), ГруппаНаименованиеКонтактногоЛица);
			
			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 24.07.2020 11:42:49
			Если пр_ЛогикаУпрощенногоВыводаЗадействована Тогда
				
				ПолеНаименование.Вид 			= ВидПоляФормы.ПолеНадписи;
				ПолеНаименование.Гиперссылка 	= Истина;
				ПолеНаименование.ЦветТекста 	= WebЦвета.Черный;
				ПолеНаименование.УстановитьДействие("Нажатие", "НаименованиеКонтактногоЛица_Открытие");

			Иначе
				
				ПолеНаименование.Вид = ВидПоляФормы.ПолеВвода;
				ПолеНаименование.ПодсказкаВвода = "Имя Фамилия";

				ПолеНаименование.КнопкаВыбора = Ложь;
				ПолеНаименование.КнопкаВыпадающегоСписка = Ложь;
				//ПолеНаименование.КнопкаСоздания = Элементы.НаименованиеКонтакт_0.КнопкаСоздания;
				//ПолеНаименование.КнопкаОткрытия = Элементы.НаименованиеКонтакт_0.КнопкаОткрытия;
				
				ПолеНаименование.УстановитьДействие("ОбработкаВыбора", "НаименованиеКонтактногоЛица_ОбработкаВыбора");
				ПолеНаименование.УстановитьДействие("НачалоВыбора", "НаименованиеКонтактногоЛица_НачалоВыбора");
				ПолеНаименование.УстановитьДействие("ИзменениеТекстаРедактирования", "НаименованиеКонтактногоЛица_ИзменениеТекстаРедактирования");
				ПолеНаименование.УстановитьДействие("ОкончаниеВводаТекста", "НаименованиеКонтактногоЛица_ОкончаниеВводаТекста");
				ПолеНаименование.УстановитьДействие("ПриИзменении", "НаименованиеКонтактногоЛица_ПриИзменении");
				ПолеНаименование.УстановитьДействие("Открытие", "НаименованиеКонтактногоЛица_Открытие");
				
			КонецЕсли; 
			// }Андрей К. 24.07.2020 11:42:49  
			
			ПолеНаименование.ПутьКДанным = "ДанныеКонтактныхЛиц[" + ИндексКонтакта + "].Наименование";
			
			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 14.08.2020 13:36:43
			//ПолеНаименование.ПоложениеЗаголовка = Элементы.НаименованиеКонтакт_0.ПоложениеЗаголовка;
			Если ЗначениеЗаполнено(ДанныеКонтактногоЛица.Должность) Тогда
				ПолеНаименование.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
				ПолеНаименование.Заголовок 			= СтрШаблон(НСтр("ru='(%1)'"), ДанныеКонтактногоЛица.Должность); 
			КонецЕсли; 
			// }Андрей К. 14.08.2020 13:36:43 
			
			ПолеНаименование.АвтоМаксимальнаяШирина = Элементы.НаименованиеКонтакт_0.АвтоМаксимальнаяШирина;
			ПолеНаименование.МаксимальнаяШирина = Элементы.НаименованиеКонтакт_0.МаксимальнаяШирина;

			КомандыСвязей = Элементы.Добавить("КоманднаяПанельСвязей_" + ИндексКонтакта, Тип("ГруппаФормы"), ГруппаНаименованиеКонтактногоЛица);
			КомандыСвязей.Вид = Элементы.КоманднаяПанельСвязей_0.Вид;
			КомандыСвязей.РастягиватьПоГоризонтали = Элементы.КоманднаяПанельСвязей_0.РастягиватьПоГоризонтали;
			
			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 24.07.2020 13:41:35
			Если пр_ЛогикаУпрощенногоВыводаЗадействована Тогда
				КомандыСвязей.Видимость = Ложь;
			КонецЕсли;
			// }Андрей К. 24.07.2020 13:41:35 
			
			ГруппаКомандСвязейСКонтактнымЛицом = Элементы.Добавить("ГруппаСвязей_" + ИндексКонтакта, Тип("ГруппаФормы"), КомандыСвязей);
			ГруппаКомандСвязейСКонтактнымЛицом.Вид = Элементы.ГруппаСвязей_0.Вид;
			ГруппаКомандСвязейСКонтактнымЛицом.Отображение = Элементы.ГруппаСвязей_0.Отображение;
			
			КнопкаУстановитьСвязь = Элементы.Добавить("УстановитьСвязь_" + ИндексКонтакта, Тип("КнопкаФормы"),ГруппаКомандСвязейСКонтактнымЛицом);
			КомандаУстановитьСвязь = ЭтаФорма.Команды.Добавить("КомандаУстановитьСвязь_" + ИндексКонтакта);
			КомандаУстановитьСвязь.Действие = "УстановитьСвязь";
			КомандаУстановитьСвязь.Подсказка = "Установить связь с контактом";
			КомандаУстановитьСвязь.ИзменяетСохраняемыеДанные = Истина;
			
			КнопкаУстановитьСвязь.ИмяКоманды = КомандаУстановитьСвязь.Имя;
			КнопкаУстановитьСвязь.Ширина = 3;
			КнопкаУстановитьСвязь.Картинка = БиблиотекаКартинок.УстановитьСвязь;
			КнопкаУстановитьСвязь.Отображение = ОтображениеКнопки.Картинка;
			
			КнопкаРазорватьСвязь = Элементы.Добавить("РазорватьСвязь_" + ИндексКонтакта, Тип("КнопкаФормы"),ГруппаКомандСвязейСКонтактнымЛицом);
			КомандаРазорватьСвязь = ЭтаФорма.Команды.Добавить("КомандаРазорватьСвязь_" + ИндексКонтакта);
			КомандаРазорватьСвязь.Действие = "РазорватьСвязь";
			КомандаРазорватьСвязь.Подсказка = "Разорвать связь с контактом";
			КомандаРазорватьСвязь.ИзменяетСохраняемыеДанные = Истина;
			
			КнопкаРазорватьСвязь.ИмяКоманды = КомандаРазорватьСвязь.Имя;
			КнопкаРазорватьСвязь.Ширина = 3;
			КнопкаРазорватьСвязь.Картинка = БиблиотекаКартинок.РазорватьСвязь;
			КнопкаРазорватьСвязь.Отображение = ОтображениеКнопки.Картинка;
			
			ГруппаНаименованиеКонтактногоЛица.ГоризонтальныйИнтервал = Элементы.ГруппаНаименованиеКонтакт_0.ГоризонтальныйИнтервал;
			
			Если ЗначениеЗаполнено(ДанныеКонтактногоЛица.КонтактноеЛицо) Тогда
				КнопкаУстановитьСвязь.Доступность = Ложь;
				КнопкаРазорватьСвязь.Доступность = Истина;
			ИначеЕсли ЗначениеЗаполнено(ДанныеКонтактногоЛица.Наименование) Тогда
				КнопкаУстановитьСвязь.Доступность = Ложь;
				КнопкаРазорватьСвязь.Доступность = Истина;
			Иначе
				КнопкаУстановитьСвязь.Доступность = Истина;
				КнопкаРазорватьСвязь.Доступность = Ложь;
			КонецЕсли;
			
			ГруппаКИ = Элементы.Добавить("КонтактнаяИнформацияКонтакт_" + ИндексКонтакта, Тип("ГруппаФормы"), ГруппаКонтактногоЛица);
			ГруппаКИ.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаКИ.Отображение = ОтображениеОбычнойГруппы.Нет;
			ГруппаКИ.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
			ГруппаКИ.ОтображатьЗаголовок = Ложь;
			
			ГруппаДобавление = Элементы.Добавить("КомандыДобавленияКонтакт_" + ИндексКонтакта, Тип("ГруппаФормы"), ГруппаКонтактногоЛица);
			ГруппаДобавление.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаДобавление.Отображение = ОтображениеОбычнойГруппы.Нет;
			ГруппаДобавление.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
			ГруппаДобавление.ОтображатьЗаголовок = Ложь;
			
			ДекорацияРастяжение = Элементы.Добавить("КомандыДобавленияРастяжение_" + ИндексКонтакта, Тип("ДекорацияФормы"), ГруппаДобавление);
			ДекорацияРастяжение.Вид = ВидДекорацииФормы.Надпись;
			ДекорацияРастяжение.АвтоМаксимальнаяШирина = Ложь;
			ДекорацияРастяжение.МаксимальнаяШирина = 41;
			ДекорацияРастяжение.РастягиватьПоГоризонтали = Истина;
			
			Кнопка = Элементы.Добавить("ДобавитьПолеКонтактнойИнформацииКонтактногоЛица_" + ИндексКонтакта, Тип("КнопкаФормы"), ГруппаДобавление);
			Кнопка.ИмяКоманды = "ДобавитьПолеКонтактнойИнформацииКонтактногоЛица";
			Кнопка.ОтображениеФигуры = ОтображениеФигурыКнопки.Нет;
			Кнопка.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
			
			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 25.07.2020 7:14:53
			ДекорацияРастяжение.Видимость = Ложь;
			Кнопка.Видимость = Ложь;
			// }Андрей К. 25.07.2020 7:14:53 
			
		Иначе
			
			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 24.07.2020 14:53:21
			Если пр_ЛогикаУпрощенногоВыводаЗадействована Тогда
				
				Если Объект.КонтактноеЛицо = ДанныеКонтактногоЛица.КонтактноеЛицо Тогда
					Элементы.Контакт_0.ЦветФона = пр_ЦветФонаГруппаОСновногоКонтактногоЛица;
				Иначе
					Элементы.Контакт_0.ЦветФона = пр_ЦветФонаГруппаКонтактногоЛица;	
				КонецЕсли; 
				
				Элементы.Контакт_0.РастягиватьПоГоризонтали 	= Ложь;
				Элементы.Контакт_0.Ширина						= пр_МаксимальнаяШиринаГруппыКИ;
				
				Элементы.НаименованиеКонтакт_0.Вид = ВидПоляФормы.ПолеНадписи;
				Элементы.НаименованиеКонтакт_0.УстановитьДействие("Нажатие", "НаименованиеКонтактногоЛица_Открытие");
				Элементы.НаименованиеКонтакт_0.Гиперссылка 				= Истина;
				Элементы.НаименованиеКонтакт_0.ЦветТекста 				= WebЦвета.Черный;
				Элементы.НаименованиеКонтакт_0.Ширина 					= пр_МаксимальнаяШиринаПредстваленияКИ;
				Элементы.НаименованиеКонтакт_0.АвтоМаксимальнаяШирина 	= Ложь;
				Элементы.НаименованиеКонтакт_0.МаксимальнаяШирина 		= пр_МаксимальнаяШиринаПредстваленияКИ;
				
				Элементы.КоманднаяПанельСвязей_0.Видимость 		= Ложь;  
				
				Если ДанныеКонтактныхЛиц.Количество() > 1 Тогда
					Элементы.КомандыДобавленияКонтакт_0.Видимость = Ложь;
				Иначе
					Элементы.ДобавитьПолеКонтактнойИнформацииКонтактногоЛица_0.Видимость = Ложь;
				КонецЕсли; 
				
				// {[+](фрагмент ДОБАВЛЕН), Андрей К. 14.08.2020 13:36:43
				Если ЗначениеЗаполнено(ДанныеКонтактногоЛица.Должность) Тогда
					Элементы.НаименованиеКонтакт_0.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
					Элементы.НаименованиеКонтакт_0.Заголовок = СтрШаблон(НСтр("ru='(%1)'"), ДанныеКонтактногоЛица.Должность); 
				КонецЕсли;
				// }Андрей К. 14.08.2020 13:36:43 
				
			КонецЕсли; 
			// }Андрей К. 24.07.2020 14:53:21 
			
			ГруппаКИ = Элементы.КонтактнаяИнформацияКонтакт_0;
			Если ЗначениеЗаполнено(ДанныеКонтактногоЛица.КонтактноеЛицо) Тогда
				Элементы.УстановитьСвязь_0.Доступность = Ложь;
				Элементы.РазорватьСвязь_0.Доступность = Истина;
			ИначеЕсли ЗначениеЗаполнено(ДанныеКонтактногоЛица.Наименование) Тогда
				Элементы.УстановитьСвязь_0.Доступность = Ложь;
				Элементы.РазорватьСвязь_0.Доступность = Истина;
			Иначе
				Элементы.УстановитьСвязь_0.Доступность = Истина;
				Элементы.РазорватьСвязь_0.Доступность = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Отбор = Новый Структура("Вид");
		
		ИндексЭлементаВГруппе = 3;
		
		Для Каждого ДанныеКИ Из ДанныеКонтактногоЛица.КонтактнаяИнформация Цикл
			
			пр_ЭтоРежимДобавленияНовойКИ = Ложь;
			
			ИндексКИ = ДанныеКонтактногоЛица.КонтактнаяИнформация.Индекс(ДанныеКИ);
			Отбор.Вид = ДанныеКИ.Вид;
			НайденныеСтроки = СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			СвойстваВида = НайденныеСтроки[0];
			
			Если ИндексЭлементаВГруппе > 2 ИЛИ НЕ пр_ЛогикаУпрощенногоВыводаЗадействована Тогда
				ГруппаЗначениеКИ = Элементы.Добавить("Контакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ГруппаФормы"), ГруппаКИ);
				ГруппаЗначениеКИ.Вид = ВидГруппыФормы.ОбычнаяГруппа;
				ГруппаЗначениеКИ.Заголовок = ДанныеКИ.Вид;
				ГруппаЗначениеКИ.Отображение = ОтображениеОбычнойГруппы.Нет;
				ГруппаЗначениеКИ.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
				ГруппаЗначениеКИ.ОтображатьЗаголовок = Ложь;
				
				// {[+](фрагмент ДОБАВЛЕН), Андрей К. 24.07.2020 14:48:54
				Если НЕ пр_ЛогикаУпрощенногоВыводаЗадействована Тогда
					ГруппаЗначениеКИ.Ширина = 35;
				КонецЕсли;
				// }Андрей К. 24.07.2020 14:48:54 
				
				ИндексЭлементаВГруппе = 1;
			КонецЕсли; 
			
			ИндексЭлементаВГруппе = ИндексЭлементаВГруппе + 1;
			 
// +CRM_УНФ
			
			//ДекорацияДействие = Элементы.Добавить("ДействиеКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ДекорацияФормы"), ГруппаЗначениеКИ);
			//ДекорацияДействие.Вид = ВидДекорацииФормы.Картинка;
			//ДекорацияДействие.Картинка = КонтактнаяИнформацияУНФ.КартинкаДействияПоТипуКонтактнойИнформации(ДанныеКИ.Тип);
			//ДекорацияДействие.Гиперссылка = Истина;
			//ДекорацияДействие.Ширина = 2;
			//ДекорацияДействие.Высота = 1;
			//ДекорацияДействие.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Авто;
			//ДекорацияДействие.УстановитьДействие("Нажатие", "Подключаемый_ДействиеКИКонтактаНажатие");
			
			ДекорацияДействие = Элементы.Добавить("ОсновнойДляСвязиКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ДекорацияФормы"), ГруппаЗначениеКИ);
			ДекорацияДействие.Вид = ВидДекорацииФормы.Картинка;
			ДекорацияДействие.Картинка = ?(ДанныеКИ.CRM_ОсновнойДляСвязи, БиблиотекаКартинок.CRM_КонтактнаяИнформацияОсновная, БиблиотекаКартинок.CRM_КонтактнаяИнформацияВторичная);
			ДекорацияДействие.Гиперссылка = Истина;
			ДекорацияДействие.Ширина = 2;
			ДекорацияДействие.Высота = 1;
			ДекорацияДействие.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Авто;
			ДекорацияДействие.УстановитьДействие("Нажатие", "Подключаемый_КонтактнаяИнформацияОсновнойДляСвязиКонтакт");

			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 24.07.2020 13:41:54
			Если пр_ЛогикаУпрощенногоВыводаЗадействована ИЛИ пр_ЭтоРежимДобавленияНовойКИ Тогда
				ДекорацияДействие.Видимость = Ложь;
			КонецЕсли; 
			// }Андрей К. 24.07.2020 13:41:54 
			
// -CRM_УНФ

			ПолеВид = Элементы.Добавить("ВидКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ПолеФормы"), ГруппаЗначениеКИ);
			ПолеВид.Вид = ВидПоляФормы.ПолеНадписи;
			ПолеВид.ПутьКДанным = "ДанныеКонтактныхЛиц[" + ИндексКонтакта + "].КонтактнаяИнформация[" + ИндексКИ + "].Вид";
			ПолеВид.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			ПолеВид.Ширина = ШиринаВидаКИ;
			ПолеВид.РастягиватьПоГоризонтали = Ложь;
			
			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 24.07.2020 13:42:12
			Если пр_ЛогикаУпрощенногоВыводаЗадействована ИЛИ пр_ЭтоРежимДобавленияНовойКИ Тогда
				ПолеВид.Видимость = Ложь;		
			КонецЕсли; 
			// }Андрей К. 24.07.2020 13:42:12 
			
			ДоступноРедактированиеВДиалоге = КонтактнаяИнформацияУНФ.ДляТипаКонтактнойИнформацииДоступноРедактированиеВДиалоге(ДанныеКИ.Тип);
			
			ПолеПредставление = Элементы.Добавить("ПредставлениеКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ПолеФормы"), ГруппаЗначениеКИ);
			
			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 24.07.2020 11:46:19
			Если пр_ЛогикаУпрощенногоВыводаЗадействована И НЕ пр_ЭтоРежимДобавленияНовойКИ Тогда
				
				Если НЕ ЗначениеЗаполнено(ДанныеКИ.Значение) Тогда
					ПолеПредставление.Видимость = Ложь;	
				КонецЕсли; 
				
				ПолеПредставление.Вид = ВидПоляФормы.ПолеНадписи;
				ПолеПредставление.Гиперссылка 				= Истина;
				ПолеПредставление.Ширина 					= пр_МаксимальнаяШиринаПредстваленияКИ;
				ПолеПредставление.АвтоМаксимальнаяШирина 	= Ложь;
				ПолеПредставление.МаксимальнаяШирина 		= пр_МаксимальнаяШиринаПредстваленияКИ;
				
				Если ДанныеКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
					ПолеПредставление.УстановитьДействие("Нажатие", "пр_Подключаемый_ПозвонитьНаНомерКонтакт");
				ИначеЕсли ДанныеКИ.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда 
					ПолеПредставление.УстановитьДействие("Нажатие", "пр_Подключаемый_ОтправитьEmailКонтакт");
				КонецЕсли;
				
			Иначе
				ПолеПредставление.Вид = ВидПоляФормы.ПолеВвода;
			КонецЕсли;
			// }Андрей К. 24.07.2020 11:46:19 		
			
			ПолеПредставление.ПутьКДанным = "ДанныеКонтактныхЛиц[" + ИндексКонтакта + "].КонтактнаяИнформация[" + ИндексКИ + "].Представление";
			ПолеПредставление.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;

			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 25.07.2020 8:01:48
			
			Если КонтактнаяИнформацияУНФ.ДляТипаКонтактнойИнформацииДоступенВводКомментария(ДанныеКИ.Тип) 
				И ЗначениеЗаполнено(ДанныеКИ.Комментарий) Тогда
				
				ПолеПредставление.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСлева;
				ПолеПредставление.Подсказка = СокрЛП(ДанныеКИ.Комментарий);
				
			КонецЕсли;
			// }Андрей К. 25.07.2020 8:01:48 
			
			Если (НЕ пр_ЛогикаУпрощенногоВыводаЗадействована) ИЛИ пр_ЭтоРежимДобавленияНовойКИ Тогда
						
				ПолеПредставление.КнопкаВыбора = ДоступноРедактированиеВДиалоге;
				ПолеПредставление.АвтоОтметкаНезаполненного = СвойстваВида.ОбязательноеЗаполнение;
				ПолеПредставление.ШиринаВыпадающегоСписка = 40;
				ПолеПредставление.УстановитьДействие("ПриИзменении", "Подключаемый_ПредставлениеКИКонтактаПриИзменении");
				ПолеПредставление.УстановитьДействие("Очистка", "Подключаемый_ПредставлениеКИКонтактаОчистка");
				
				Если СвойстваВида.Тип = Перечисления.ТипыКонтактнойИнформации.Другое Тогда
					Если СвойстваВида.ВидПоляДругое = "МногострочноеШирокое" Тогда
						ПолеПредставление.МногострочныйРежим = Истина;
						ПолеПредставление.АвтомаксимальнаяВысота = Ложь;
						ПолеПредставление.МаксимальнаяВысота = 2;
					КонецЕсли;
					Если СвойстваВида.ВидПоляДругое = "ОднострочноеУзкое" Тогда
						ПолеПредставление.АвтоМаксимальнаяШирина = Ложь;
						ПолеПредставление.МаксимальнаяШирина = 27;
					КонецЕсли;
				КонецЕсли;
				Если СвойстваВида.ВидРедактирования = "Диалог" И ДанныеКИ.Тип <> Перечисления.ТипыКонтактнойИнформации.ВебСтраница Тогда
					ПолеПредставление.РедактированиеТекста = Ложь;
					ПолеПредставление.ЦветФона = ЦветаСтиля.КонтактнаяИнформацияСРедактированиемВДиалогеЦвет;
				КонецЕсли;
				Если ДоступноРедактированиеВДиалоге Тогда
					ПолеПредставление.УстановитьДействие("НачалоВыбора", "Подключаемый_ПредставлениеКИКонтактаНачалоВыбора");
				КонецЕсли;
				
			КонецЕсли; 

			Если КонтактнаяИнформацияУНФ.ДляТипаКонтактнойИнформацииДоступенВводКомментария(ДанныеКИ.Тип) Тогда
				
				ПолеКомментарий = Элементы.Добавить("КомментарийКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ПолеФормы"), ГруппаЗначениеКИ);
				ПолеКомментарий.Вид = ВидПоляФормы.ПолеНадписи;
				ПолеКомментарий.ПутьКДанным = "ДанныеКонтактныхЛиц[" + ИндексКонтакта + "].КонтактнаяИнформация[" + ИндексКИ + "].Комментарий";
				ПолеКомментарий.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
				ПолеКомментарий.АвтоМаксимальнаяШирина = Ложь;
				
				// {[+](фрагмент ДОБАВЛЕН), Андрей К. 24.07.2020 13:49:44
				Если пр_ЛогикаУпрощенногоВыводаЗадействована ИЛИ пр_ЭтоРежимДобавленияНовойКИ Тогда
					ПолеКомментарий.Видимость = Ложь;
				Иначе
					
					ПолеПредставление.АвтоМаксимальнаяШирина = Ложь;
					ПолеПредставление.МаксимальнаяШирина = 27;
					
					ПолеКомментарий.Вид = ВидПоляФормы.ПолеВвода;
					ПолеКомментарий.ПропускатьПриВводе = Истина;
					ПолеКомментарий.ПодсказкаВвода = НСтр("ru='Прим.'");
					ПолеКомментарий.АвтоМаксимальнаяШирина = Ложь;
					// +CRM_УНФ
					//ПолеКомментарий.МаксимальнаяШирина = ШиринаПоляКомментария;
					Если ДанныеКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
						ПолеКомментарий.МаксимальнаяШирина = ШиринаПоляКомментария-3;
					Иначе
						ПолеКомментарий.МаксимальнаяШирина = ШиринаПоляКомментария;
					КонецЕсли;
					// -CRM_УНФ
					ПолеКомментарий.УстановитьДействие("ПриИзменении", "Подключаемый_КомментарийКИКонтактаПриИзменении");
	
				КонецЕсли; 
				// }Андрей К. 24.07.2020 13:49:44 
				
			КонецЕсли;
// +CRM_УНФ
			Если ДанныеКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
				ДекорацияДействие = Элементы.Добавить("ДействиеКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ + "_1", Тип("ДекорацияФормы"), ГруппаЗначениеКИ);
				ДекорацияДействие.Вид = ВидДекорацииФормы.Картинка;
				ДекорацияДействие.Картинка = БиблиотекаКартинок.CRM_SMSСообщение;
				ДекорацияДействие.Гиперссылка = Истина;
				ДекорацияДействие.Ширина = 2;
				ДекорацияДействие.Высота = 1;
				ДекорацияДействие.УстановитьДействие("Нажатие", "Подключаемый_ОтправитьСМСНаНомерКонтакт");
				ДекорацияДействие.Подсказка = "Отправить SMS сообщение на данный номер телефона";
				
				// {[+](фрагмент ДОБАВЛЕН), Андрей К. 24.07.2020 13:43:33
				Если пр_ЛогикаУпрощенногоВыводаЗадействована ИЛИ пр_ЭтоРежимДобавленияНовойКИ Тогда
					ДекорацияДействие.Видимость = Ложь;
				КонецЕсли; 
				// }Андрей К. 24.07.2020 13:43:33 
				
			КонецЕсли;
			
			ДекорацияДействие = Элементы.Добавить("ДействиеКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ + "_2", Тип("ДекорацияФормы"), ГруппаЗначениеКИ);
			ДекорацияДействие.Вид = ВидДекорацииФормы.Картинка;
			ДекорацияДействие.Картинка = КонтактнаяИнформацияУНФ.КартинкаДействияПоТипуКонтактнойИнформации(ДанныеКИ.Тип);
			ДекорацияДействие.Гиперссылка = Истина;
			ДекорацияДействие.Ширина = 2;
			ДекорацияДействие.Высота = 1;
			Если ДанныеКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
				ДекорацияДействие.УстановитьДействие("Нажатие", "Подключаемый_ПозвонитьНаНомерКонтакт");
				ДекорацияДействие.Подсказка = "Позвонить на данный номер телефона";
			ИначеЕсли ДанныеКИ.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
				ДекорацияДействие.УстановитьДействие("Нажатие", "Подключаемый_ОтправитьEmailКонтакт");
				ДекорацияДействие.Подсказка = "Отправить Email по данному адресу";
			Иначе
				ДекорацияДействие.УстановитьДействие("Нажатие", "Подключаемый_ДействиеКИКонтактаНажатие");
			КонецЕсли;
			
			// {[+](фрагмент ДОБАВЛЕН), Андрей К. 24.07.2020 12:27:32
			Если пр_ЛогикаУпрощенногоВыводаЗадействована ИЛИ пр_ЭтоРежимДобавленияНовойКИ Тогда
				ДекорацияДействие.Видимость = Ложь;
			КонецЕсли; 
			// }Андрей К. 24.07.2020 12:27:32 

// -CRM_УНФ
		КонецЦикла;
		
	КонецЦикла;
	
	ЗаполнитьСписокВыбораАдресовКонтактныхЛиц(ЭтотОбъект);
	
	// ОбменСGoogle.ПодключитьОбработчикиСобытияАвтоподбор(ЭтотОбъект, ОписаниеПолейДляАвтоподбора());
	
	ИндексПоследнегоКЛ = ДанныеКонтактныхЛиц.Количество()-1;
	КомандаДобавленияКИ = Элементы["ДобавитьПолеКонтактнойИнформацииКонтактногоЛица_" + ИндексПоследнегоКЛ];
	Элементы.Переместить(Элементы.ДобавитьПоляКонтактногоЛица, КомандаДобавленияКИ.Родитель, КомандаДобавленияКИ);
	Элементы["КомандыДобавленияРастяжение_" + ИндексПоследнегоКЛ].МаксимальнаяШирина = 34;
	ДобавитьЭлементыДублей();
	
КонецПроцедуры

&НаСервере
&Вместо("ОписаниеПолейДляАвтоподбора")
Функция пр_ОписаниеПолейДляАвтоподбора()
	// Вставить содержимое метода.
	// Результат = ПродолжитьВызов();
	
	Результат = Новый Соответствие;

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура пр_НаименованиеКонтакт_0НажатиеПосле(Элемент, СтандартнаяОбработка)
	
	НаименованиеКонтактногоЛица_Открытие(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте 
Процедура пр_Подключаемый_ПозвонитьНаНомерКонтакт(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяЭлемента = СокрЛП(СтрЗаменить(Элемент.Имя, "ПредставлениеКонтакт_", "ДействиеКонтакт_")) + "_2";
	
	Подключаемый_ПозвонитьНаНомерКонтакт(Элементы[ИмяЭлемента]);
	
КонецПроцедуры

&НаКлиенте 
Процедура пр_Подключаемый_ОтправитьEmailКонтакт(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Подключаемый_ОтправитьEmailКонтакт(Элемент);
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти





