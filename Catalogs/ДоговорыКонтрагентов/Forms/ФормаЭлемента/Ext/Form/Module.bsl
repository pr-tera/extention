
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура пр_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	пр_СоздатьНаФормеРеквизитыФранчайзи(); 	
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПриОткрытииПосле(Отказ)
	
	пр_УправлениеВидимостьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "пр_ИзменениеРеквизитовДоговораКонтрагента" Тогда
		
		Модифицированность = Истина;
		ЗаполнитьЗначенияСвойств(Объект, Параметр);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура пр_КатегорияДоговораПриИзмененииПосле(Элемент)
	
	пр_УправлениеВидимостьюЭлементовФормы();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнениеПоШаблонуВидаВзаиморасчетов", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='Заполнить договор по шаблону?'"), РежимДиалогаВопрос.ДаНет, 60);

КонецПроцедуры

&НаКлиенте
Процедура пр_ОповещатьКлиентаОПревышенииЧасовПриИзменении(Элемент)
	
	пр_УправлениеВидимостьюЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура пр_СоздатьНаФормеРеквизитыФранчайзи()
	
	// Вид взаиморасчетов
	КонтекстПоляГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ЛеваяКолонка, Элементы.Родитель);
	КонтекстПоляГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
	ГруппаВидВзаиморасчетов = РедакторФорм.ДобавитьГруппуОбычную(КонтекстПоляГруппы, "пр_ГруппаВедениеВзаиморасчетов");
	
	СвойстваПоляСтоимость = Новый Структура;
	СвойстваПоляСтоимость.Вставить("Заголовок", "руб. / час");
	СвойстваПоляСтоимость.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Право);
	КонтекстПоляСтоимость = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, ГруппаВидВзаиморасчетов, , СвойстваПоляСтоимость);
	ПолеСтоимость = РедакторФорм.ДобавитьРеквизитОбъектаНаФорму(КонтекстПоляСтоимость, "франСтоимостьЧасаРаботИсполнителя"); 
	
	// Отчетность по закрытым обращениям
	СвойстваФлажка = Новый Структура;
	СвойстваФлажка.Вставить("Заголовок", "Отчетность по закрытым обращениям");
	СвойстваФлажка.Вставить("Подсказка", "Выписывать финансовые документы только на основании закрытых обращений и за весь период работ по ним.");
	СвойстваФлажка.Вставить("ОтображениеПодсказки", ОтображениеПодсказки.Кнопка);
	
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ЛеваяКолонка, Элементы.Родитель, СвойстваФлажка);	
	ПолеФлажка = РедакторФорм.ДобавитьПолеФлажкаНаФормуРеквизитОбъекта(КонтекстЭлемента, "пр_ОтчетностьПоЗакрытымОбращениям"); 
	
	// Ведение взаиморасчетов
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ЛеваяКолонка, Элементы.Родитель);	
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Ведение взаиморасчетов");
	ПолеФормы = РедакторФорм.ДобавитьРеквизитОбъектаНаФорму(КонтекстЭлемента, "ВедениеВзаиморасчетов");

	// фран Тип договора
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ЛеваяКолонка, Элементы.Родитель);	
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Тип договора");
	ПолеФормы = РедакторФорм.ДобавитьРеквизитОбъектаНаФорму(КонтекстЭлемента, "франТипДоговора");
	
	// Оповещение о превышении часов
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Заголовок", 				"Превышение часов");
	СвойстваЭлемента.Вставить("Группировка", 			ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	СвойстваЭлемента.Вставить("Поведение", 				ПоведениеОбычнойГруппы.Свертываемая);
	СвойстваЭлемента.Вставить("ОтображатьЗаголовок", 	Истина);
	СвойстваЭлемента.Вставить("ОтображениеУправления", 	ОтображениеУправленияОбычнойГруппы.Картинка);
	СвойстваЭлемента.Вставить("Отображение", 			ОтображениеОбычнойГруппы.СлабоеВыделение);
	КонтекстПоляГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ПраваяКолонка, Элементы.ГруппаКомментарий, СвойстваЭлемента);
	ГруппаОповещениеПользователя = РедакторФорм.ДобавитьГруппуОбычную(КонтекстПоляГруппы, "пр_ГруппаОповещениеПользователя");
	
	// пр_ОповещатьКлиентаОПревышенииЧасов
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Заголовок", "Оповещать клиента о превышении часов");
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, ГруппаОповещениеПользователя, , СвойстваЭлемента);
	ФлажокОповещатьКлиентаОПревышенииЧасов = РедакторФорм.ДобавитьПолеФлажкаНаФормуРеквизитОбъекта(
		КонтекстЭлемента, "пр_ОповещатьКлиентаОПревышенииЧасов");
	ФлажокОповещатьКлиентаОПревышенииЧасов.Установитьдействие("ПриИзменении", "пр_ОповещатьКлиентаОПревышенииЧасовПриИзменении");
		
	// КаналОповещения
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Заголовок", "КаналОповещения");
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, ГруппаОповещениеПользователя, , СвойстваЭлемента);
	ПолеКаналОповещения	= РедакторФорм.ДобавитьПолеНаФормуРеквизитОбъекта(КонтекстЭлемента, "пр_КаналОповещения");
	
	// пр_ПредставлениеКаналаОповещения
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Заголовок", "Адрес");
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, ГруппаОповещениеПользователя, , СвойстваЭлемента);
	ПолеПредставлениеКаналаОповещения = РедакторФорм.ДобавитьПолеНаФормуРеквизитОбъекта(
		КонтекстЭлемента, "пр_ПредставлениеКаналаОповещения");
		
	// пр_КритическоеКоличествоЧасовДляОповещения
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Заголовок", "Критическое количество часов для оповещения");
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, ГруппаОповещениеПользователя, , СвойстваЭлемента);
	ПолеПредставлениеКаналаОповещения = РедакторФорм.ДобавитьПолеНаФормуРеквизитОбъекта(
		КонтекстЭлемента, "пр_КритическоеКоличествоЧасовДляОповещения");
	
КонецПроцедуры

&НаКлиенте
Процедура пр_УправлениеВидимостьюЭлементовФормы()
	
	ЭтоДоговорПоФакту = Объект.КатегорияДоговора = ПредопределенноеЗначение("Справочник.КатегорииДоговоров.прПоФакту");
	
	Элементы.франСтоимостьЧасаРаботИсполнителя.Видимость = ЭтоДоговорПоФакту;
	Элементы.франСтоимостьЧасаРаботИсполнителя.Родитель.РастягиватьПоГоризонтали = ?(ЭтоДоговорПоФакту, Ложь, Неопределено);
	
	Элементы.пр_КаналОповещения.Видимость 							= Объект.пр_ОповещатьКлиентаОПревышенииЧасов;
	Элементы.пр_ПредставлениеКаналаОповещения.Видимость 			= Объект.пр_ОповещатьКлиентаОПревышенииЧасов;
	Элементы.пр_КритическоеКоличествоЧасовДляОповещения.Видимость 	= Объект.пр_ОповещатьКлиентаОПревышенииЧасов;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеПоШаблонуВидаВзаиморасчетов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;	
	КонецЕсли; 
	
	Успех = ЗаполнитьРеквизитыДоговораПоШаблонуНаСервере();
	
	Если Успех Тогда
		ПоказатьОповещениеПользователя(НСтр("ru='Заполнение по шаблону'"), , НСтр("ru='Выполнено'"), БиблиотекаКартинок.Успешно);
	Иначе
		Текст = НСтр("ru='Договор не заполнен. Не найден шаблон договора, проверьте вид взаиморасчетов и вид договора'"); 
		ОбщегоНазначенияКлиент.СообщитьПользователю(Текст);
	КонецЕсли; 
	
КонецПроцедуры 

&НаСервере
Функция ЗаполнитьРеквизитыДоговораПоШаблонуНаСервере()
	
	Возврат пр_Служебные.ЗаполнитьРеквизитыДоговораПоШаблону(Объект.ВидДоговора, Объект.КатегорияДоговора, ЭтотОбъект);
	
КонецФункции

#КонецОбласти





