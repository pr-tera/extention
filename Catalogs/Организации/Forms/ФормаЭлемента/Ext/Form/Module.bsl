#Область ОбработчикиСобытийФормы

&НаСервере
Процедура пр_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	пр_СоздатьФранРеквизитыФормы(); 	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура пр_СоздатьФранРеквизитыФормы()
		
	ИмяГруппы = "пр_ГруппаРеквизитыФранчайзи";
	
	пр_ГруппаФормы = Элементы.Вставить(ИмяГруппы, Тип("ГруппаФормы"), Элементы.СписокРеквизитов, Элементы.ЮридическиеДанные);
	пр_ГруппаФормы.Заголовок 				= "Франчайзи";
	пр_ГруппаФормы.Вид 						= ВидГруппыФормы.ОбычнаяГруппа;
	пр_ГруппаФормы.Поведение 				= ПоведениеОбычнойГруппы.Свертываемая;
	пр_ГруппаФормы.Группировка 				= ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	пр_ГруппаФормы.Объединенная 			= Истина;
	пр_ГруппаФормы.ОтображениеУправления 	= ОтображениеУправленияОбычнойГруппы.Картинка;
	пр_ГруппаФормы.Отображение 				= ОтображениеОбычнойГруппы.СильноеВыделение;
	
	пр_СоздатьЭлементФормы("франКодПартнера1С", 	пр_ГруппаФормы, "Объект.франКодПартнера1С", "Код партнера 1С");
	пр_СоздатьЭлементФормы("франЯвляетсяДочернейФирмой1С", пр_ГруппаФормы, "Объект.франЯвляетсяДочернейФирмой1С", 
		"Является дочерней Фирмой 1С");
	пр_СоздатьЭлементФормы("франКонтрагент", 		пр_ГруппаФормы, "Объект.франКонтрагент", "Контрагент");
	пр_СоздатьЭлементФормы("ПРКодОрганизацииМС", 	пр_ГруппаФормы, "Объект.ПРКодОрганизацииМС", "Код организации МС");

КонецПроцедуры

&НаСервере
Процедура пр_СоздатьЭлементФормы(ИмяЭлемента, ГруппаФормы, ПутьКДанным, ЗаголовокЭлемента)
	
	Если Элементы.Найти(ИмяЭлемента) = Неопределено Тогда
		
		ЭлементФормы = Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), ГруппаФормы);  
		
		ЭлементФормы.Вид 			= ВидПоляФормы.ПолеВвода;
		ЭлементФормы.ПутьКДанным 	= ПутьКДанным;
		ЭлементФормы.Заголовок 		= ЗаголовокЭлемента;
		
	КонецЕсли;   
		
КонецПроцедуры

#КонецОбласти
  

