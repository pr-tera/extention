#Область ОбработчикиСобытийФормы

&НаСервере
Процедура пр_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	пр_УправлениеРеквизитамиФормы(); 	
	пр_УправлениеСвйоствамиЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура пр_КатегорияНоменклатурыПриИзмененииПосле(Элемент)
	
	пр_УправлениеСвйоствамиЭлементовФормы();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура пр_УправлениеРеквизитамиФормы()
	
	//Тип программного объекта
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ОсновныеРеквизиты,
		Элементы.Наименование); 
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Тип ПО");
	ЭлементФормы = РедакторФорм.ДобавитьРеквизитОбъектаНаФорму(КонтекстЭлемента, "пр_ТипПрограммногоОбъекта"); 
	
	// Группа пр_ГруппаРеквизитыФранчайзи
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Заголовок", 				"Франчайзи");
	СвойстваЭлемента.Вставить("Вид", 					ВидГруппыФормы.ОбычнаяГруппа);
	СвойстваЭлемента.Вставить("Поведение", 				ПоведениеОбычнойГруппы.Свертываемая);
	СвойстваЭлемента.Вставить("Группировка", 			ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	СвойстваЭлемента.Вставить("Объединенная", 			Истина);
	СвойстваЭлемента.Вставить("ОтображениеУправления", 	ОтображениеУправленияОбычнойГруппы.Картинка);
	СвойстваЭлемента.Вставить("Отображение",			ОтображениеОбычнойГруппы.ОбычноеВыделение);
	
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.СвернутыеГруппы,
		Элементы.ГруппаУчетнаяИнформация, СвойстваЭлемента); 
	
	пр_ГруппаФормы = РедакторФорм.ДобавитьГруппуОбычную(КонтекстЭлемента, "пр_ГруппаРеквизитыФранчайзи");
	
	// Бесплатная подписка ИТС 
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаФормы);
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Бесплатная подписка ИТС");
	ЭлементФормы = РедакторФорм.ДобавитьПолеФлажкаНаФормуРеквизитОбъекта(КонтекстЭлемента, "франБесплатнаяПодпискаИТС"); 
	
	// Вид подписки ИТС
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Вид подписки ИТС");
	ЭлементФормы = РедакторФорм.ДобавитьРеквизитОбъектаНаФорму(КонтекстЭлемента, "франВидПодпискиИТС"); 
	
	//Производитель 
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Производитель");
	ЭлементФормы = РедакторФорм.ДобавитьРеквизитОбъектаНаФорму(КонтекстЭлемента, "франПроизводитель"); 
	
	// Группа прГруппаРегистрацияПродаж
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Вид", 					ВидГруппыФормы.ОбычнаяГруппа);
	СвойстваЭлемента.Вставить("Группировка", 			ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	СвойстваЭлемента.Вставить("Отображение", 			ОтображениеОбычнойГруппы.ОбычноеВыделение);
	СвойстваЭлемента.Вставить("ОтображатьЗаголовок", 	Ложь);
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаФормы, , СвойстваЭлемента); 

	прГруппаПР = РедакторФорм.ДобавитьГруппуОбычную(КонтекстЭлемента, "прГруппаРегистрацияПродаж");
	
	// Регистрация продаж
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, прГруппаПР);
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Регистрация продаж");
	ЭлементФормы = РедакторФорм.ДобавитьПолеФлажкаНаФормуРеквизитОбъекта(КонтекстЭлемента, "франРегистрацияПродаж"); 
	
	// Пакет часов
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Пакет часов");
	ЭлементФормы = РедакторФорм.ДобавитьПолеФлажкаНаФормуРеквизитОбъекта(КонтекстЭлемента, "прПакетЧасов"); 
	
	// Телефония
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Телефония");
	ЭлементФормы = РедакторФорм.ДобавитьПолеФлажкаНаФормуРеквизитОбъекта(КонтекстЭлемента, "Телефония"); 
	
	// Автор
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Автор");
	ЭлементФормы = РедакторФорм.ДобавитьРеквизитОбъектаНаФорму(КонтекстЭлемента, "франАвтор"); 
	
	// Дата создания
	КонтекстЭлемента.Свойства.Вставить("Заголовок", "Дата создания");
	ЭлементФормы = РедакторФорм.ДобавитьРеквизитОбъектаНаФорму(КонтекстЭлемента, "франДатаСоздания"); 
	
КонецПроцедуры

&НаСервере
Процедура пр_УправлениеСвйоствамиЭлементовФормы()
	
	ЭтоПО = Объект.КатегорияНоменклатуры = Справочники.КатегорииНоменклатуры.пр_ПрограммныйПродуктПраво 
		ИЛИ	Объект.КатегорияНоменклатуры = Справочники.КатегорииНоменклатуры.пр_СобственныеРазработки;

	Элементы.пр_ТипПрограммногоОбъекта.Видимость = ЭтоПО;
	Элементы.пр_ТипПрограммногоОбъекта.АвтоОтметкаНезаполненного = ЭтоПО;
	
КонецПроцедуры

#КонецОбласти


