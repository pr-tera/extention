#Область ПрограммныйИнтерфейс

// Функция формирует таблицу начислений с суммами для заполнения документов
// "Прием на работу" и "Кадровое перемещение".
// ОписаниеМетода3
//
// Параметры:
//  СсылкаНаДокумент 	- ДокументСсылка.ПриемНаРаботу - документ источник.
//            			- ДокументСсылка.КадровоеПеремещение - документ источник.
//
//	ПараметрыМетода - Структура - коллекция дополнителних параметров:
//	 * ДопПараметр1  - Тип1ДопПараметра1 - ОписаниеТип1ДопПараметра1
//	 * ДопПараметр2  - Тип1ДопПараметра2 - ОписаниеТип1ДопПараметра2
//	 * ДопПараметр3  - Тип1ДопПараметра3 - ОписаниеТип1ДопПараметра3
//	 * ДопПараметр4  - Тип1ДопПараметра4 - ОписаниеТип1ДопПараметра4
//	 * ДопПараметр5  - Тип1ДопПараметра5 - ОписаниеТип1ДопПараметра5
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица, соответствующая табличной части "НачисленияУдержания" документа "Прием на работу"
//
Функция ПолучитьТаблицуНачисленийПоГрейдамСотрудников(СсылкаНаДокумент) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.пр_Грейд КАК пр_Грейд,
		|	ТаблицаДокумента.КлючСвязи КАК КлючСвязи,
		|	&СчетЗатрат КАК СчетЗатрат,
		|	&Валюта КАК Валюта
		|ПОМЕСТИТЬ Сотрудники
		|ИЗ
		|	Документ.ПриемНаРаботу.Сотрудники КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	пр_ГрейдыСпециалистов.Ссылка КАК Грейд,
		|	&ВидНачисленияОклад КАК ВидНачисленияОклад,
		|	пр_ГрейдыСпециалистов.Оклад КАК Оклад,
		|	&ВидНачисленияПремия4Плюс КАК ВидНачисленияПремия4Плюс,
		|	пр_ГрейдыСпециалистов.Премия4Плюс КАК Премия4Плюс,
		|	&ВидНачисленияПремияЗаРезультат КАК ВидНачисленияПремияЗаРезультат,
		|	пр_ГрейдыСпециалистов.ПремияЗаРезультат КАК ПремияЗаРезультат
		|ПОМЕСТИТЬ ПоказателиГрейдов
		|ИЗ
		|	Справочник.пр_ГрейдыСпециалистов КАК пр_ГрейдыСпециалистов
		|ГДЕ
		|	пр_ГрейдыСпециалистов.Ссылка В
		|			(ВЫБРАТЬ
		|				Сотрудники.пр_Грейд КАК пр_Грейд
		|			ИЗ
		|				Сотрудники КАК Сотрудники)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиГрейдовОклад.ВидНачисленияОклад КАК ВидНачисленияУдержания,
		|	ПоказателиГрейдовОклад.Оклад КАК Сумма,
		|	Сотрудники.Валюта КАК Валюта,
		|	Сотрудники.СчетЗатрат КАК СчетЗатрат,
		|	Сотрудники.КлючСвязи КАК КлючСвязи,
		|	Сотрудники.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ПредварительныеДанные
		|ИЗ
		|	Сотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоказателиГрейдов КАК ПоказателиГрейдовОклад
		|		ПО Сотрудники.пр_Грейд = ПоказателиГрейдовОклад.Грейд
		|			И (ПоказателиГрейдовОклад.ВидНачисленияОклад <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ПустаяСсылка))
		|			И (ПоказателиГрейдовОклад.Оклад > 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоказателиГрейдовПремия4Плюс.ВидНачисленияПремия4Плюс,
		|	ПоказателиГрейдовПремия4Плюс.Премия4Плюс,
		|	Сотрудники.Валюта,
		|	Сотрудники.СчетЗатрат,
		|	Сотрудники.КлючСвязи,
		|	Сотрудники.НомерСтроки
		|ИЗ
		|	Сотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоказателиГрейдов КАК ПоказателиГрейдовПремия4Плюс
		|		ПО Сотрудники.пр_Грейд = ПоказателиГрейдовПремия4Плюс.Грейд
		|			И (ПоказателиГрейдовПремия4Плюс.ВидНачисленияПремия4Плюс <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ПустаяСсылка))
		|			И (ПоказателиГрейдовПремия4Плюс.Премия4Плюс > 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоказателиГрейдовПремияЗаРезультат.ВидНачисленияПремияЗаРезультат,
		|	ПоказателиГрейдовПремияЗаРезультат.ПремияЗаРезультат,
		|	Сотрудники.Валюта,
		|	Сотрудники.СчетЗатрат,
		|	Сотрудники.КлючСвязи,
		|	Сотрудники.НомерСтроки
		|ИЗ
		|	Сотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоказателиГрейдов КАК ПоказателиГрейдовПремияЗаРезультат
		|		ПО Сотрудники.пр_Грейд = ПоказателиГрейдовПремияЗаРезультат.Грейд
		|			И (ПоказателиГрейдовПремияЗаРезультат.ВидНачисленияПремияЗаРезультат <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ПустаяСсылка))
		|			И (ПоказателиГрейдовПремияЗаРезультат.ПремияЗаРезультат > 0)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Актуальность КАК Актуальность,
		|	ПредварительныеДанные.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
		|	ПредварительныеДанные.Сумма КАК Сумма,
		|	ПредварительныеДанные.Валюта КАК Валюта,
		|	ПредварительныеДанные.СчетЗатрат КАК СчетЗатрат,
		|	ПредварительныеДанные.КлючСвязи КАК КлючСвязи
		|ИЗ
		|	ПредварительныеДанные КАК ПредварительныеДанные
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПредварительныеДанные.НомерСтроки";
	
	Запрос.УстановитьПараметр("Валюта", 		Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("Ссылка", 		СсылкаНаДокумент);
	Запрос.УстановитьПараметр("СчетЗатрат", 	ПланыСчетов.Управленческий.УправленческиеРасходы);
	Запрос.УстановитьПараметр("Актуальность", 	Истина);
	
	Запрос.УстановитьПараметр("ВидНачисленияОклад", 			Константы.пр_ВидНачисленияОклад.Получить());
	Запрос.УстановитьПараметр("ВидНачисленияПремия4Плюс", 		Константы.пр_ВидНачисленияПремия4Плюс.Получить());
	Запрос.УстановитьПараметр("ВидНачисленияПремияЗаРезультат", Константы.пр_ВидНачисленияПремияЗаРезультат.Получить());
	
	Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.КадровоеПеремещение") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.ПриемНаРаботу.Сотрудники", "Документ.КадровоеПеремещение.Сотрудники");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Актуальность КАК Актуальность,", "");
	КонецЕсли; 
	
	Возврат Запрос.Выполнить().Выгрузить();
		
КонецФункции

#КонецОбласти

