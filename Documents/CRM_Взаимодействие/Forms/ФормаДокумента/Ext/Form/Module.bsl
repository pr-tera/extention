#Область ОбработчикиСобытийФормы

&НаСервере
Процедура пр_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	пр_УправлениеЭлементамиФормы();
	
	пр_УстановитьПараметрыДинамическихСписков();
	пр_ОбновитьИзображения1СКоннект();
	пр_УстановитьУсловноеОформление();
	
	Если ЗначениеЗаполнено(Объект.Обращение) Тогда
		Элементы.ДекорацияОбращение.Заголовок = Строка(Объект.Обращение);	
	КонецЕсли; 
	
	пр_ОбновитьТелефонКонтрагента();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПриОткрытииПосле(Отказ)
	
	пр_ОбновитьИнформациюОДоговоре();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УстановленНовыйОтветственный" Тогда
		
		Объект.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияСобытий.Запланировано");

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура пр_Подключаемый_ДекорацияОбращениеНажатие(Элемент)
	
	пр_РаботаСОбращениямиКлиент.ВвестиОбращение(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура пр_Подключаемый_ИнформацияОДоговореНажатие(Элемент)
	
	Если НЕ пр_ДоговорКонтрагента.Пустая() Тогда
		
		ПоказатьЗначение(, пр_ДоговорКонтрагента); 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура пр_Подключаемый_ИнформацияОЧасахНажатие(Элемент)
	
	ПоказатьПредупреждение(, НСтр("ru='Данный функционал еще не задействовован после переходна на УНФ.'")); 
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПартнерПриИзмененииПосле(Элемент)
	
	пр_ПартнерПриИзмененииНаСервере();
	пр_ОбновитьИнформациюОДоговоре();

КонецПроцедуры

&НаСервере
Процедура пр_ПартнерПриИзмененииНаСервере()
	
	пр_ОбновитьИзображения1СКоннект();
	пр_ОбновитьТелефонКонтрагента();
	пр_ОбновитьТелефонКонтактногоЛица();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_КонтактноеЛицоПриИзмененииПосле(Элемент)
	
	пр_КонтактноеЛицоПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура пр_КонтактноеЛицоПриИзмененииНаСервере()
	
	пр_ОбновитьИзображения1СКоннект();
	пр_ОбновитьТелефонКонтактногоЛица();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_Подключаемый_ТелефонКонтактногоЛицаНажатие(Элемент)
	
	пр_ПозвонитьПоНомеруТелефона(Элемент.Заголовок);
	
КонецПроцедуры

&НаКлиенте
Процедура пр_Подключаемый_ТелефонКонтрагентНажатие(Элемент)
	
	пр_ПозвонитьПоНомеруТелефона(Элемент.Заголовок);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_пр_ВыбораДоговораШагНазад(Команда)
	
	пр_ОтобразитьИнформациюПоСледующемуДоговору("Предыдущий");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_пр_ВыбораДоговораШагВперед(Команда)
	
	пр_ОтобразитьИнформациюПоСледующемуДоговору("Следующий");
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПривязатьОбращение(Команда)
	
	ТекущиеДанные = Элементы.пр_Обращения.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 	
		Возврат;
	КонецЕсли; 
	
	пр_ПривязатьОбращениеНаСервере(ТекущиеДанные.Ссылка);
	
	Элементы.ДекорацияОбращение.Заголовок = ТекущиеДанные.Ссылка;
	
	Модифицированность = Истина;
	Объект.ГруппаСобытия 	= пр_ГруппаСобытий1яЛиния();
	Объект.Обращение 		= ТекущиеДанные.Ссылка;
	Объект.Тема				= "Повторное обращение. " + СокрЛП(Объект.Тема);
	
КонецПроцедуры

// Проверяет состояние события, и если оно Завершено или Отменено устанавливает его на "В работе".
// Добавляет сообщение в чат об изменении статуса
//
// Параметры:
//  Обращение - ДокументСсылка.прОбращение 
// 
&НаСервереБезКонтекста
Процедура пр_ПривязатьОбращениеНаСервере(Обращение)
	
	СостояниеОбращения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Обращение, "Состояние", Истина); 
	
	Если СостояниеОбращения = Перечисления.СостоянияСобытий.Завершено 
		Или СостояниеОбращения = Перечисления.СостоянияСобытий.Отменено Тогда
		
		ОбращениеОбъект = Обращение.ПолучитьОбъект();
		ОбращениеОбъект.Состояние = Перечисления.СостоянияСобытий.CRM_ВРаботе;
		ОбращениеОбъект.Записать();
		
		пр_Служебные.ДобавитьКомментарийКДокументу(Обращение, "Обращение переведено в работу",
			Пользователи.ТекущийПользователь(), Истина, Перечисления.ПР_ТипыСообщенийЧата.ЗаборИзПоиска);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Аннотация не требуется, см. имя метода
//
// Параметры:
//  Нет
//    
&НаСервере
Процедура пр_УстановитьУсловноеОформление()
	
	// Состояние Завершено
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "пр_Обращения.Состояние", Перечисления.СостоянияСобытий.Завершено);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "пр_Обращения");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветФона", WebЦвета.СветлоЗеленый);
	
	// Состояние CRM_Отложено или Отменено
	Состояния = Новый Массив;
	Состояния.Добавить(Перечисления.СостоянияСобытий.CRM_Отложено);
	Состояния.Добавить(Перечисления.СостоянияСобытий.Отменено);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "пр_Обращения.Состояние", Состояния, ВидСравненияКомпоновкиДанных.ВСписке);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "пр_Обращения");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветФона", WebЦвета.СеребристоСерый);
	
КонецПроцедуры

// Аннотация не требуется, см. имя метода
//
// Параметры:
//  Нет
//    
&НаСервере
Процедура пр_УстановитьПараметрыДинамическихСписков()
	
	// Обращения
	Если Объект.Партнер.Пустая() Тогда
		СвязанныеКонтрагенты = Новый Массив;
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПРСвязанныеКонтрагенты.СвязанныйКонтрагент КАК СвязанныйКонтрагент
			|ИЗ
			|	РегистрСведений.ПРСвязанныеКонтрагенты КАК ПРСвязанныеКонтрагенты
			|ГДЕ
			|	ПРСвязанныеКонтрагенты.Контрагент = &Контрагент
			|	И ПРСвязанныеКонтрагенты.ДатаОкончания >= &ДатаОкончания
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Контрагенты.Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.Ссылка = &Контрагент";
		
		Запрос.УстановитьПараметр("ДатаОкончания", 	ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("Контрагент", 	Объект.Партнер);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		СвязанныеКонтрагенты = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("СвязанныйКонтрагент");
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(пр_Обращения, "Контрагент", 
		СвязанныеКонтрагенты, , , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный); 
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(пр_Обращения, "ПометкаУдаления", 
		Ложь, , , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный); 
		
	// Задания
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(пр_Задания, "Контрагент", 
		Объект.Партнер, , , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный); 
		
КонецПроцедуры

#Область УправлениеЭлементамиФормы

// Добавление всех нестандартных элементов на форму, а так же перемещение или
// скрытие стандартных. 
//
// Параметры:
//  Нет
//    
&НаСервере
Процедура пр_УправлениеЭлементамиФормы()
	
	пр_ДобавитьЭлементыДляКонтрагента();
	пр_ДобавитьЭлементыДляКонтактногоЛица();
	пр_ДобавитьГруппуЭлементовДоговорИТС();
	пр_ДобавитьГруппуЭлементовДекорацияОбращение();
	
	пр_ДобавитьНаФормуРеквизитыФранчайзи();
	
КонецПроцедуры

// Аннотация не требуется, см. имя метода
//
// Параметры:
//  Нет
// 
&НаСервере 
Процедура пр_ДобавитьЭлементыДляКонтрагента()
	
	// Добавиление изображения коннект
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ГруппаЛеваяКолонка, Элементы.Партнер);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
	
 	пр_ГруппаКонтрагент = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "пр_ГруппаКонтрагент");
	
	КонтекстПоля = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаКонтрагент);	
	
	пр_КартинкаКоннектКонтрагент = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстПоля, "пр_КартинкаКоннектКонтрагент");
	пр_КартинкаКоннектКонтрагент.Картинка = БиблиотекаКартинок.пр_КоннектОтсутствует;
	
	Элементы.Переместить(Элементы.Партнер, пр_ГруппаКонтрагент);
	
	// Добавиление декарация телефон
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Заголовок", 				пр_ПустойНомерТелефона());
	СвойстваЭлемента.Вставить("АвтоМаксимальнаяШирина", Ложь);
	СвойстваЭлемента.Вставить("Ширина", 				15);
	СвойстваЭлемента.Вставить("Гиперссылка", 			Истина);
	КонтекстДекарации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаКонтрагент, , СвойстваЭлемента);	
	
	ДекарацияТелефонКонтрагента =  РедакторФорм.ДобавитьДекорациюНадписьНаФорму(КонтекстДекарации, "пр_ТелефонКонтрагента");
	ДекарацияТелефонКонтрагента.УстановитьДействие("Нажатие", "пр_Подключаемый_ТелефонКонтрагентНажатие");
	
КонецПроцедуры

// Аннотация не требуется, см. имя метода
//
// Параметры:
//  Нет
// 
&НаСервере 
Процедура пр_ДобавитьГруппуЭлементовДекорацияОбращение()
	
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, ЭтаФорма, Элементы.ОткрытьВКалендаре);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
	
 	пр_ГруппаДекорацияОбращение = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "пр_ДекорацияОбращение");
	
	СвойстваДекорации = Новый Структура;
	СвойстваДекорации.Вставить("Заголовок", 				"Ввести документ ""Обращение""");
	СвойстваДекорации.Вставить("Гиперссылка", 				Истина);
	СвойстваДекорации.Вставить("РастягиватьПоГоризонтали", 	Истина);
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаДекорацияОбращение, , СвойстваДекорации);	
	
	пр_ДекорацияОбращение = РедакторФорм.ДобавитьДекорациюНадписьНаФорму(КонтекстДекорации, "ДекорацияОбращение");
	пр_ДекорацияОбращение.УстановитьДействие("Нажатие", "пр_Подключаемый_ДекорацияОбращениеНажатие");

	Элементы.Переместить(Элементы.ОткрытьВКалендаре, пр_ГруппаДекорацияОбращение);
	
КонецПроцедуры

// Аннотация не требуется, см. имя метода
//
// Параметры:
//  Нет
//
&НаСервере 
Процедура пр_ДобавитьЭлементыДляКонтактногоЛица()
	
	// Добавиление изображения коннект
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ГруппаЛеваяКолонка, Элементы.КонтактноеЛицо);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
	
 	пр_ГруппаКонтактноеЛицо = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "пр_ГруппаКонтактноеЛицо");
	
	КонтекстПоля = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаКонтактноеЛицо);	
	
	пр_КартинкаКоннектКонтактноеЛицо = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстПоля, "пр_КартинкаКоннектКонтактноеЛицо");
	пр_КартинкаКоннектКонтактноеЛицо.Картинка = БиблиотекаКартинок.пр_КоннектОтсутствует;
	
	Элементы.Переместить(Элементы.КонтактноеЛицо, пр_ГруппаКонтактноеЛицо);
	
	// Добавиление декарация телефон
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Заголовок", 				пр_ПустойНомерТелефона());
	СвойстваЭлемента.Вставить("АвтоМаксимальнаяШирина", Ложь);
	СвойстваЭлемента.Вставить("Ширина", 				15);
	СвойстваЭлемента.Вставить("Гиперссылка", 			Истина);
	КонтекстДекарации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаКонтактноеЛицо, , СвойстваЭлемента);	
	
	ДекарацияТелефонКонтактногоЛица =  РедакторФорм.ДобавитьДекорациюНадписьНаФорму(КонтекстДекарации, "пр_ТелефонКонтактногоЛица");
	ДекарацияТелефонКонтактногоЛица.УстановитьДействие("Нажатие", "пр_Подключаемый_ТелефонКонтактногоЛицаНажатие");

КонецПроцедуры

// Аннотация не требуется, см. имя метода
//
// Параметры:
//  Нет
//    
&НаСервере
Процедура пр_ДобавитьГруппуЭлементовДоговорИТС()
	
	// Корневая группа
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ГруппаГлавное, Элементы.Содержание);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
 	пр_ГруппаДоговорИТС = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "пр_ГруппаДоговорИТС");

	
	// Группа кнопок переключения договоров лево/право.
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаДоговорИТС);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
 	пр_ГруппаСтрелкиВыбораДоговора = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "ГруппаСтрелкиВыбораДоговора");
	
	// Лево
	КонтекстКоманды = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаСтрелкиВыбораДоговора);	
	пр_ВыбораДоговораШагНазад = РедакторФорм.ДобавитьКомандуИКнопкуНаФорму(КонтекстКоманды, "пр_ВыбораДоговораШагНазад", "Шаг назад");
	пр_ВыбораДоговораШагНазад.Картинка = БиблиотекаКартинок.ПРШагНазад;
	пр_ВыбораДоговораШагНазад.Отображение = ОтображениеКнопки.Картинка;
	
	// Право
	КонтекстКоманды = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаСтрелкиВыбораДоговора);	
	пр_ВыбораДоговораШагВперед = РедакторФорм.ДобавитьКомандуИКнопкуНаФорму(КонтекстКоманды, "пр_ВыбораДоговораШагВперед", "Шаг назад");
	пр_ВыбораДоговораШагВперед.Картинка = БиблиотекаКартинок.пр_ШагВперед;
	пр_ВыбораДоговораШагВперед.Отображение = ОтображениеКнопки.Картинка;
	
	
	// Группа декораций линий, фреш, информация по договору.
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаДоговорИТС);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
 	пр_ГруппаОписаниеДоговора = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "ГруппаОписаниеДоговора");
	
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	пр_ДекорацияЛиния1Есть = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстДекорации, "ДекорацияЛиния1Есть");
	пр_ДекорацияЛиния1Есть.Картинка = БиблиотекаКартинок.пр_Линия1Есть;
	
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	пр_ДекорацияЛиния1Нет = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстДекорации, "ДекорацияЛиния1Нет");
	пр_ДекорацияЛиния1Нет.Картинка = БиблиотекаКартинок.пр_Линия1Нет;
	
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	пр_ДекорацияЛиния2Есть = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстДекорации, "ДекорацияЛиния2Есть");
	пр_ДекорацияЛиния2Есть.Картинка = БиблиотекаКартинок.пр_Линия2Есть;
	
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	пр_ДекорацияЛиния2Нет = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстДекорации, "ДекорацияЛиния2Нет");
	пр_ДекорацияЛиния2Нет.Картинка = БиблиотекаКартинок.пр_Линия2Нет;
	
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	пр_ДекорацияФреш = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстДекорации, "ДекорацияФреш");
	пр_ДекорацияФреш.Картинка = БиблиотекаКартинок.пр_Фреш;
	
	СвойстваДекорации = Новый Структура;
	СвойстваДекорации.Вставить("Заголовок", 	"Информация о договоре");
	СвойстваДекорации.Вставить("Гиперссылка", 	Истина);
	СвойстваДекорации.Вставить("РастягиватьПоГоризонтали", 	Истина);
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора, , СвойстваДекорации);	
	пр_ИнформацияОДоговоре = РедакторФорм.ДобавитьДекорациюНадписьНаФорму(КонтекстДекорации, "ИнформацияОДоговоре");
	пр_ИнформацияОДоговоре.УстановитьДействие("Нажатие", "пр_Подключаемый_ИнформацияОДоговореНажатие");
	
	КонтекстПоля = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	КонтекстПоля.Свойства.Вставить("ПутьКДанным", 	"Объект.Партнер");
	КонтекстПоля.Свойства.Вставить("Видимость", 	Ложь);
	ПолеФормы = РедакторФорм.ДобавитьПолеНаФорму(КонтекстПоля, "ДоговорКонтрагента");
	
	
	// Группа декораций информация о часах
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаДоговорИТС);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
 	пр_ГруппаЧасы = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "ГруппаЧасы");
	
	СвойстваДекорации = Новый Структура;
	СвойстваДекорации.Вставить("Заголовок", 	"Информация о часах");
	СвойстваДекорации.Вставить("Гиперссылка", 	Истина);
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора, , СвойстваДекорации);	
	пр_ИнформацияОЧасах = РедакторФорм.ДобавитьДекорациюНадписьНаФорму(КонтекстДекорации, "ИнформацияОЧасах");
	пр_ИнформацияОЧасах.УстановитьДействие("Нажатие", "пр_Подключаемый_ИнформацияОЧасахНажатие");

КонецПроцедуры

// Аннотация не требуется, см. имя метода
//
// Параметры:
//  Нет
//    
&НаСервере
Процедура пр_ДобавитьНаФормуРеквизитыФранчайзи()
	
		// Корневая группа
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ГруппаРеквизиты);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Истина);
	КонтекстГруппы.Свойства.Вставить("Заголовок", 			"Франчайзи");
	КонтекстГруппы.Свойства.Вставить("Группировка",			ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
 	пр_ГруппаРеквизитыФранчайзи = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "пр_ГруппаРеквизитыФранчайзи");
	
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаРеквизитыФранчайзи);
	ЭлементФормы = РедакторФорм.ДобавитьПолеНаФормуРеквизитОбъекта(КонтекстЭлемента, "ГруппаСобытия");
	
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаРеквизитыФранчайзи);
	ЭлементФормы = РедакторФорм.ДобавитьПолеНаФормуРеквизитОбъекта(КонтекстЭлемента, "ПРВидОбращения");
	ЭлементФормы.Заголовок = "Вид обращения";
	
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаРеквизитыФранчайзи);
	ЭлементФормы = РедакторФорм.ДобавитьПолеФлажкаНаФормуРеквизитОбъекта(КонтекстЭлемента, "НесколькоКонтрагентов");
	
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаРеквизитыФранчайзи);
	ЭлементФормы = РедакторФорм.ДобавитьМногострочноеПолеРеквизитОбъекта(КонтекстЭлемента, "ПричинаРазблокировкиОбращения");

	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаРеквизитыФранчайзи);
	ЭлементФормы = РедакторФорм.ДобавитьМногострочноеПолеРеквизитОбъекта(КонтекстЭлемента, "ПРАдресПолучения");
	
КонецПроцедуры

// Аннотация не требуется, см. имя метода
//
// Параметры:
//  Нет.
// 
&НаКлиенте
Процедура пр_УправлениеДекорациямиДоговора()
	
	ПараметрыОбъекта = пр_ПереходНаУНФКлиент.ПараметрыОбъектаФормыДляУправленияДекорациями();
	
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Контрагент 		= Объект.Партнер;
	ПараметрыОбъекта.ДоговорКонтрагента = пр_ДоговорКонтрагента;
	ПараметрыОбъекта.ВидОбращения 		= Объект.ПРВидОбращения;
	
	пр_ПереходНаУНФКлиент.УправлениеДекорациямиДоговора(ЭтотОбъект, ПараметрыОбъекта); 
	
КонецПроцедуры

#КонецОбласти 

// Метод изменяет реквизит формы "пр_ДоговорКонтрагента", перемещая текущий договор
// в рамках таблицы договоров "пр_СписокДоговоров". 
//
// Параметры:
//  Нет
//    
&НаКлиенте
Процедура пр_ОтобразитьИнформациюПоСледующемуДоговору(Направление)
	
	ПараметрыОтбора = Новый Структура("ДоговорКонтрагента", пр_ДоговорКонтрагента);
	ТекущаяСтрокаВТаблице = пр_СписокДоговоров.НайтиСтроки(ПараметрыОтбора);  
	
	Если ТекущаяСтрокаВТаблице.Количество() = 0 Тогда
		ВызватьИсключение(НСтр("ru='Ошибка поиска позиции текущего договора.'"));	
	КонецЕсли; 
	
	ТекущийИдентификатор = ТекущаяСтрокаВТаблице[0].ПолучитьИдентификатор();
	
	Если Направление = "Предыдущий" Тогда
		НовыйИдентификатор = Макс(0, ТекущийИдентификатор - 1); 
	Иначе  		
		НовыйИдентификатор = Мин(пр_СписокДоговоров.Количество() - 1, ТекущийИдентификатор + 1); 
	КонецЕсли; 
	
	Если пр_ДоговорКонтрагента <> пр_СписокДоговоров[НовыйИдентификатор].ДоговорКонтрагента Тогда
		пр_ДоговорКонтрагента = пр_СписокДоговоров[НовыйИдентификатор].ДоговорКонтрагента; 
		пр_УправлениеДекорациямиДоговора();	
	КонецЕсли; 

КонецПроцедуры

// Управление декорациями и изображениями по договорам контрагента
//
// Параметры:
//  Нет.
//
&НаКлиенте 
Процедура пр_ОбновитьИнформациюОДоговоре()
	
	Если НЕ Объект.Партнер.Пустая() Тогда
		пр_ПолучитьСписокДоговоровКонтрагента();
	Иначе
		пр_СписокДоговоров.Очистить();
	КонецЕсли; 
	
	Элементы.ГруппаСтрелкиВыбораДоговора.Видимость = пр_СписокДоговоров.Количество() > 1;
	
	пр_УправлениеДекорациямиДоговора();
	
КонецПроцедуры

// Аннотация не требуется, см. имя метода
//
// Параметры:
//  Нет.
// 
&НаСервере
Процедура пр_ПолучитьСписокДоговоровКонтрагента()
	
	пр_СписокДоговоров.Загрузить(пр_Служебные.ПолучитьСписокДоговоров(Объект.Партнер, Объект.Дата, Истина));
	Если пр_СписокДоговоров.Количество() > 0 Тогда
		пр_ДоговорКонтрагента = пр_СписокДоговоров[0].ДоговорКонтрагента;
	Иначе
		пр_ДоговорКонтрагента = "";
	КонецЕсли; 
	
КонецПроцедуры

// Процедура обновляет картинки прКартинкаКоннектКонтрагент и прКартинкаКоннектКонтактноеЛицо
// в зависимости от доступности контрагенту или контаткному лицу сервиса 1С Коннект
//
// Параметры:
//  Нет 
//
&НаСервере 
Процедура пр_ОбновитьИзображения1СКоннект()
	
	КонтрагентуДосутпен1СКоннект 		= Ложь;
	КонтактномуЛицуДосутпен1СКоннект 	= Ложь;
	
	Если НЕ Объект.Партнер.Пустая() Тогда
		
		// Метод из расширения конфигурации
		КонтрагентуДосутпен1СКоннект = 
			РегистрыСведений.эсКоннектСоответствиеКлиентовКонтрагентам.пр_КонтрагентуДоступен1СКоннект(Объект.Партнер);	
			
		// Метод из расширения конфигурации
		КонтактномуЛицуДосутпен1СКоннект = 
			РегистрыСведений.эсКоннектСоответствиеСотрудниковКлиентовКонтактнымЛицам.пр_КонтактномуЛицуДоступен1СКоннект(Объект.КонтактноеЛицо);	
			
	КонецЕсли; 
	
	Элементы.пр_КартинкаКоннектКонтрагент.Картинка = 
		?(КонтрагентуДосутпен1СКоннект,
		БиблиотекаКартинок.бфКоннект, 
		БиблиотекаКартинок.пр_КоннектОтсутствует);
		
	Элементы.пр_КартинкаКоннектКонтактноеЛицо.Картинка = 
		?(КонтактномуЛицуДосутпен1СКоннект,
		БиблиотекаКартинок.бфКоннект, 
		БиблиотекаКартинок.пр_КоннектОтсутствует);
		
КонецПроцедуры

// Метод возвращает группу событий с наименованием "1-я линия (Повторная)"
//
// Параметры:
//  Нет 
//
// Возвращаемое значение:
//  СправочникСсылка.ГруппыСобытий
//          
&НаСервереБезКонтекста
Функция пр_ГруппаСобытий1яЛиния()
	
	Возврат Справочники.ГруппыСобытий.НайтиПоНаименованию("1-я линия (Повторная)");
	
КонецФункции

&НаКлиенте
Процедура пр_ПозвонитьПоНомеруТелефона(НомерТелефона)
	
	Если Не ЗначениеЗаполнено(НомерТелефона) Или НомерТелефона = пр_ПустойНомерТелефона() Тогда
		Возврат;	
	КонецЕсли; 
	
	ДанныеЗаполнения = Новый Структура;
	Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.CRM_Интерес") Тогда
		ДанныеЗаполнения.Вставить("Основание",	Объект.ДокументОснование);	
	Иначе
		ДанныеЗаполнения.Вставить("Основание",	Объект.Ссылка);	
	КонецЕсли;
	
	ДанныеЗаполнения.Вставить("Взаимодействие", Объект.Ссылка);
	
	сфпСофтФонПроКлиент.сфпПозвонить(НомерТелефона, Объект.КонтактноеЛицо, ДанныеЗаполнения);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция пр_ПустойНомерТелефона()
	
	Возврат "Телефон не найден";
	
КонецФункции

&НаСервере
Процедура пр_ОбновитьТелефонКонтрагента()
	
	Если Объект.Партнер.Пустая() Тогда
		Телефон = пр_ПустойНомерТелефона();
	Иначе
		Телефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Объект.Партнер, 
			Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента, ТекущаяДатаСеанса(), Истина);
			
		Если Не ЗначениеЗаполнено(Телефон) Тогда
			Телефон = пр_ПустойНомерТелефона();	
		КонецЕсли; 
		
	КонецЕсли; 
	
	Элементы.пр_ТелефонКонтрагента.Заголовок = СокрЛП(Телефон);
	
КонецПроцедуры

&НаСервере
Процедура пр_ОбновитьТелефонКонтактногоЛица()
	
	Если Объект.КонтактноеЛицо.Пустая() Тогда
		Телефон = пр_ПустойНомерТелефона();
	Иначе
		
		Телефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Объект.КонтактноеЛицо, 
			Справочники.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица, ТекущаяДатаСеанса(), Истина);
			
		Если Не ЗначениеЗаполнено(Телефон) Тогда
			Телефон = пр_ПустойНомерТелефона();	
		КонецЕсли; 
	
	КонецЕсли; 
	
	Элементы.пр_ТелефонКонтактногоЛица.Заголовок = СокрЛП(Телефон);
	
КонецПроцедуры

#КонецОбласти


