#Область ОбработчикиСобытийФормы

&НаСервере
Процедура пр_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	пр_УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПриОткрытииПосле(Отказ)
	
	пр_УправлениеВидимостьюЭлементовФормы();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура пр_ВидОперацииПриИзмененииПосле(Элемент)
	
	пр_УправлениеВидимостьюЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗапасы

&НаКлиенте
Процедура пр_ЗапасыПриИзмененииПосле(Элемент)

	ТекущиеДанные = Элементы.Запасы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 	
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.СчетЗатрат) И ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		
		ТекущиеДанные.СчетЗатрат = пр_ПереходНаУНФВызовСервера.СчетУчетаЗатратНоменклатуры(ТекущиеДанные.Номенклатура);	
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура пр_УправлениеЭлементамиФормы()
	
	// Добавление в табличную часть "Запасы" Поля "СчетЗатрат"
	КонтекстПоля = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.Запасы);	
	КонтекстПоля.Свойства.Вставить("ПутьКДанным", 	"Объект.Запасы.СчетЗатрат");
	КонтекстПоля.Свойства.Вставить("Заголовок", 	"Счет затрат");
	КонтекстПоля.Свойства.Вставить("Доступность", 	Истина);
	
	ПолеФормы = РедакторФорм.ДобавитьПолеНаФорму(КонтекстПоля, "ЗапасыСчетЗатрат");
	
	// Переход с УПП
	пр_РаботаСФормами.пр_ДобавитьФлажкиПринадлежностиУчетовНаФорму(ЭтаФорма, Элементы.ГруппаЛево);
		
КонецПроцедуры

&НаКлиенте
Процедура пр_УправлениеВидимостьюЭлементовФормы()
	
	Элементы.ЗапасыСчетЗатрат.Видимость = 
		Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПеремещениеЗапасов.СписаниеНаРасходы");
	
КонецПроцедуры

#КонецОбласти


