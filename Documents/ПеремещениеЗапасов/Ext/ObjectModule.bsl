
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
     	
#Область ОбработчикиСобытий

&Перед("ПередЗаписью")
Процедура пр_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОтражатьВУправленческомУчете И Не ОтражатьВБухгалтерскомУчете Тогда
		Организация = Справочники.Организации.ОсновнаяОрганизация;	
	КонецЕсли; 
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли; 
	
	ЗаполнитьСчетаУчетаЗапасов();
	
КонецПроцедуры

&Перед("ПриКопировании")
Процедура пр_ПриКопировании(ОбъектКопирования)
	
	ОтражатьВБухгалтерскомУчете 	= Ложь;	
	ОтражатьВУправленческомУчете 	= Ложь;
	
КонецПроцедуры

&Вместо("ОбработкаПроведения")
Процедура пр_ОбработкаПроведения(Отказ, РежимПроведения)
	
	// переопределение методов проведения документа для загруженных из УПП документов
	Если НЕ ОтражатьВУправленческомУчете И ОтражатьВБухгалтерскомУчете Тогда
		пр_ПереопределенноеПроведениеДокумента(Отказ, РежимПроведения);
	Иначе
		ПродолжитьВызов(Отказ, РежимПроведения);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

Процедура пр_ПереопределенноеПроведениеДокумента(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей.
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

Процедура ЗаполнитьСчетаУчетаЗапасов()
	
	ЭтоПеремещение = ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.Перемещение;
	
	Для каждого СтрокаЗапасы Из Запасы Цикл
		
		Если ЭтоПеремещение Тогда
			
			СтрокаЗапасы.СчетЗатрат = ПланыСчетов.Управленческий.ПустаяСсылка();
			
		Иначе
			
			Если Не ЗначениеЗаполнено(СтрокаЗапасы.СчетЗатрат) И ЗначениеЗаполнено(СтрокаЗапасы.Номенклатура) Тогда
				
				СтрокаЗапасы.СчетЗатрат = пр_ПереходНаУНФВызовСервера.СчетУчетаЗатратНоменклатуры(СтрокаЗапасы.Номенклатура);	
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли 
