#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
     	
#Область СлужебныеПроцедурыИФункции

&ИзменениеИКонтроль("ИнициализироватьДанныеДокумента")
Процедура пр_ИнициализироватьДанныеДокумента(ДокументСсылкаКадровоеПеремещение, СтруктураДополнительныеСвойства)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	КадровоеПеремещениеСотрудники.НомерСтроки,
	|	КадровоеПеремещениеСотрудники.Сотрудник,
	|	КадровоеПеремещениеСотрудники.СтруктурнаяЕдиница,
	|	КадровоеПеремещениеСотрудники.Должность,
	|	КадровоеПеремещениеСотрудники.ГрафикРаботы,
	|	КадровоеПеремещениеСотрудники.ЗанимаемыхСтавок,
	|	КадровоеПеремещениеСотрудники.Период, 	
	#Вставка 
	|	КадровоеПеремещениеСотрудники.пр_Грейд КАК пр_Грейд,
	#КонецВставки
	|	КадровоеПеремещениеСотрудники.Ссылка
	|ПОМЕСТИТЬ ТаблицаСотрудники
	|ИЗ
	|	Документ.КадровоеПеремещение.Сотрудники КАК КадровоеПеремещениеСотрудники
	|ГДЕ
	|	КадровоеПеремещениеСотрудники.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	КадровоеПеремещениеСотрудники.НомерСтроки,
	|	КадровоеПеремещениеСотрудники.Сотрудник,
	|	КадровоеПеремещениеСотрудники.Период,
	|	КадровоеПеремещениеНачисленияУдержания.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|	КадровоеПеремещениеНачисленияУдержания.Валюта,
	|	КадровоеПеремещениеНачисленияУдержания.Сумма КАК Сумма,
	|	КадровоеПеремещениеНачисленияУдержания.СчетЗатрат,
	|	КадровоеПеремещениеНачисленияУдержания.Актуальность
	|ПОМЕСТИТЬ ТаблицаНачисленияУдержания
	|ИЗ
	|	Документ.КадровоеПеремещение.Сотрудники КАК КадровоеПеремещениеСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КадровоеПеремещение.НачисленияУдержания КАК КадровоеПеремещениеНачисленияУдержания
	|		ПО КадровоеПеремещениеСотрудники.КлючСвязи = КадровоеПеремещениеНачисленияУдержания.КлючСвязи
	|ГДЕ
	|	КадровоеПеремещениеСотрудники.Ссылка = &Ссылка
	|	И КадровоеПеремещениеНачисленияУдержания.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	КадровоеПеремещениеСотрудники.НомерСтроки,
	|	КадровоеПеремещениеСотрудники.Сотрудник,
	|	КадровоеПеремещениеСотрудники.Период,
	|	КадровоеПеремещениеНалогиНаДоходы.ВидНачисленияУдержания,
	|	КадровоеПеремещениеНалогиНаДоходы.Валюта,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	КадровоеПеремещениеНалогиНаДоходы.Актуальность
	|ИЗ
	|	Документ.КадровоеПеремещение.Сотрудники КАК КадровоеПеремещениеСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КадровоеПеремещение.НалогиНаДоходы КАК КадровоеПеремещениеНалогиНаДоходы
	|		ПО КадровоеПеремещениеСотрудники.КлючСвязи = КадровоеПеремещениеНалогиНаДоходы.КлючСвязи
	|ГДЕ
	|	КадровоеПеремещениеСотрудники.Ссылка = &Ссылка
	|	И КадровоеПеремещениеНалогиНаДоходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСотрудники.Организация,
	|	ТаблицаСотрудники.НомерСтроки,
	|	ТаблицаСотрудники.Сотрудник,
	|	ТаблицаСотрудники.СтруктурнаяЕдиница,
	|	ТаблицаСотрудники.Должность,
	|	ТаблицаСотрудники.ГрафикРаботы,
	|	ТаблицаСотрудники.ЗанимаемыхСтавок,
	#Вставка 
	|	ТаблицаСотрудники.пр_Грейд КАК пр_Грейд,
	#КонецВставки
	|	ТаблицаСотрудники.Период
	|ИЗ
	|	ТаблицаСотрудники КАК ТаблицаСотрудники
	|ГДЕ
	|	ТаблицаСотрудники.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКадровоеПеремещение.ПеремещениеИИзменениеСпособаОплаты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНачисленияУдержания.Организация,
	|	ТаблицаНачисленияУдержания.НомерСтроки,
	|	ТаблицаНачисленияУдержания.Сотрудник,
	|	ТаблицаНачисленияУдержания.Период,
	|	ТаблицаНачисленияУдержания.ВидНачисленияУдержания,
	|	ТаблицаНачисленияУдержания.Валюта,
	|	ТаблицаНачисленияУдержания.Сумма,
	|	ТаблицаНачисленияУдержания.СчетЗатрат,
	|	ТаблицаНачисленияУдержания.Актуальность
	|ИЗ
	|	ТаблицаНачисленияУдержания КАК ТаблицаНачисленияУдержания");

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКадровоеПеремещение);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	МассивРезультатов = Запрос.ВыполнитьПакет();

	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСотрудники", 				МассивРезультатов[2].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПлановыеНачисленияИУдержания", МассивРезультатов[3].Выгрузить());
	#Вставка 
		пр_ЗаполнитьТаблицуПоказателиKPI(ДокументСсылкаКадровоеПеремещение, СтруктураДополнительныеСвойства);
	#КонецВставки
	
	СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;

КонецПроцедуры

Процедура пр_ЗаполнитьТаблицуПоказателиKPI(ДокументСсылкаКадровоеПеремещение, СтруктураДополнительныеСвойства)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КадровоеПеремещениеСотрудники.Период КАК Период,
		|	КадровоеПеремещениеСотрудники.Сотрудник КАК Сотрудник,
		|	КадровоеПеремещениеСотрудники.Сотрудник.Физлицо КАК ФизическоеЛицо,
		|	КадровоеПеремещениеСотрудники.пр_НормаЧасовМинимум КАК НормаЧасовМинимум,
		|	КадровоеПеремещениеСотрудники.пр_НормаЧасовМаксимум КАК НормаЧасовМаксимум,
		|	КадровоеПеремещениеСотрудники.пр_Оклад КАК Оклад,
		|	КадровоеПеремещениеСотрудники.пр_ПремияЗаРезультат КАК ПремияЗаРезультат,
		|	КадровоеПеремещениеСотрудники.пр_Премия4Плюс КАК Премия4Плюс,
		|	КадровоеПеремещениеСотрудники.пр_КонтролироватьЗаполнениеОтчета КАК КонтролироватьЗаполнениеОтчета,
		|	КадровоеПеремещениеСотрудники.пр_КоэффициентРабочегоВремениУменьшенный КАК КоэффициентРабочегоВремениУменьшенный,
		|	КадровоеПеремещениеСотрудники.пр_КоэффициентРабочегоВремени КАК КоэффициентРабочегоВремени,
		|	КадровоеПеремещениеСотрудники.пр_КоэффициентРабочегоВремениУвеличенный КАК КоэффициентРабочегоВремениУвеличенный,
		|	ИСТИНА КАК РазрешитьПоискИсполнителя,
		|	КадровоеПеремещениеСотрудники.пр_Руководитель КАК Руководитель,
		|	КадровоеПеремещениеСотрудники.пр_НеЗаполнятьТипЗадания КАК НеЗаполнятьТипЗадания
		|ИЗ
		|	Документ.КадровоеПеремещение.Сотрудники КАК КадровоеПеремещениеСотрудники
		|ГДЕ
		|	КадровоеПеремещениеСотрудники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКадровоеПеремещение);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПоказателиKPI", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли 
