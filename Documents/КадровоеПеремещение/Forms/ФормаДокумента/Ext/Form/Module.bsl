#Область ОбработчикиСобытийФормы

&НаСервере
Процедура пр_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	
	пр_ИзменениеРеквизитовФормы();
	пр_ВосстановитьНастройкуОтображенияДопРеквизитов();

КонецПроцедуры

&НаКлиенте
Процедура пр_ПриОткрытииПосле(Отказ)
	
	пр_РаботаСФормамиКлиент.УправлениеВидимостьюДополнительныхЭлементовФормыКадровогоДокумена(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПриЗакрытииПосле(ЗавершениеРаботы)
	
	пр_СохранитьНастройкуОтображенияДопРеквизитов(пр_ОтображатьДополнительныеКолонки);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура пр_ЗаполнитьИзГрейдовПеред(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("пр_ЗаполнениеНачисленийУдержаний", ЭтотОбъект);
	
	Если Объект.НачисленияУдержания.Количество() > 0 Тогда
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='При заполнении все текущие начисления будут очищены. Продолжить?'"), 
			РежимДиалогаВопрос.ДаНетОтмена, 60);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);	
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура пр_ПоказатьСкрытьДополнительныеРеквизитыПосле(Команда)
	
	пр_ОтображатьДополнительныеКолонки = Не пр_ОтображатьДополнительныеКолонки;
	пр_РаботаСФормамиКлиент.УправлениеВидимостьюДополнительныхЭлементовФормыКадровогоДокумена(ЭтаФорма);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура пр_ИзменениеРеквизитовФормы()
	
	пр_РаботаСФормами.ДобавитьРеквизитыТЧСотрудникиКадровыхДокументов(ЭтаФорма, Элементы.Сотрудники);
	
КонецПроцедуры

&НаСервереБезКонтекста
&ИзменениеИКонтроль("ПолучитьДанныеСотрудника")
Процедура пр_ПолучитьДанныеСотрудника(СтруктураСотрудник)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СотрудникиСрезПоследних.СтруктурнаяЕдиница,
	|	СотрудникиСрезПоследних.Должность,
	|	СотрудникиСрезПоследних.ЗанимаемыхСтавок,
	#Вставка 
	|	СотрудникиСрезПоследних.пр_Грейд,
	#КонецВставки 
	|	СотрудникиСрезПоследних.ГрафикРаботы
	|ИЗ
	|	РегистрСведений.Сотрудники.СрезПоследних(
	|			&Период,
	|			Сотрудник = &Сотрудник
	|				И Организация = &Организация) КАК СотрудникиСрезПоследних");

	Запрос.УстановитьПараметр("Период", СтруктураСотрудник.Период);
	Запрос.УстановитьПараметр("Сотрудник", СтруктураСотрудник.Сотрудник);
	Запрос.УстановитьПараметр("Организация", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(СтруктураСотрудник.Организация));

	СтруктураСотрудник.Вставить("СтруктурнаяЕдиница");
	СтруктураСотрудник.Вставить("Должность");
	СтруктураСотрудник.Вставить("ЗанимаемыхСтавок", 1);
	СтруктураСотрудник.Вставить("ГрафикРаботы"); 
	#Вставка
	СтруктураСотрудник.Вставить("пр_Грейд"); 
	#КонецВставки 

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураСотрудник, Выборка);
	КонецЦикла;

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ЗаполнитьЗначения")
Процедура пр_ЗаполнитьЗначения()

	Если Объект.Сотрудники.Количество() = 0 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийКадровоеПеремещение.ИзменениеСпособаОплаты Тогда
		Возврат;
	КонецЕсли;

	Для каждого СтрокаТЧ Из Объект.Сотрудники Цикл

		СтруктураСотрудник = Новый Структура;
		СтруктураСотрудник.Вставить("Период", СтрокаТЧ.Период - 1);
		СтруктураСотрудник.Вставить("Сотрудник", СтрокаТЧ.Сотрудник);
		СтруктураСотрудник.Вставить("Организация", Объект.Организация);

		ПолучитьДанныеСотрудника(СтруктураСотрудник);

		СтрокаТЧ.ПрежнееПодразделение = СтруктураСотрудник.СтруктурнаяЕдиница;
		СтрокаТЧ.ПрежняяДолжность = СтруктураСотрудник.Должность;
		СтрокаТЧ.ПрежнееКоличествоЗанимаемыхСтавок = СтруктураСотрудник.ЗанимаемыхСтавок;
		СтрокаТЧ.ПрежнийГрафикРаботы = СтруктураСотрудник.ГрафикРаботы;
		#Вставка 
		СтрокаТЧ.пр_СтарыйГрейд = СтруктураСотрудник.пр_Грейд;
		#КонецВставки 

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура пр_ЗаполнениеНачисленийУдержаний(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		пр_ЗаполнитьНачисленияПослеПослеНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура пр_ЗаполнитьНачисленияПослеПослеНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.пр_ЗаполнитьНачисленияПоГрейдам();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура пр_СохранитьНастройкуОтображенияДопРеквизитов(Настройка)
	
	ХранилищеОбщихНастроек.Сохранить("Документ.КадровоеПеремещение", "ОтображатьДополнительныеКолонки", Настройка);

КонецПроцедуры

&НаСервере
Процедура пр_ВосстановитьНастройкуОтображенияДопРеквизитов()
	
	пр_ОтображатьДополнительныеКолонки = ХранилищеОбщихНастроек.Загрузить("Документ.КадровоеПеремещение", "ОтображатьДополнительныеКолонки");

КонецПроцедуры

#КонецОбласти



