#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйИнтерфейс

&После("ИнициализироватьДанныеДокумента")
Процедура пр_ИнициализироватьДанныеДокумента(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	пр_ИнициализироватьТаблицуДвиженийНачисленныеБаллы(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);	
	
КонецПроцедуры

Процедура пр_ИнициализироватьТаблицуДвиженийНачисленныеБаллы(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ПриходнаяНакладная.Ответственный.Физлицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК Ответственный,
		|	ПриходнаяНакладная.Дата КАК Дата
		|ИЗ
		|	Документ.ПриходнаяНакладная КАК ПриходнаяНакладная
		|ГДЕ
		|	ПриходнаяНакладная.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	СтруктураШапкиДокумента = Новый Структура;
	СтруктураШапкиДокумента.Вставить("Ссылка",			ДокументСсылкаПриходнаяНакладная);
	СтруктураШапкиДокумента.Вставить("Ответственный", 	Выборка.Ответственный);
	СтруктураШапкиДокумента.Вставить("Автор", 			Выборка.Ответственный);
	СтруктураШапкиДокумента.Вставить("Дата", 			Выборка.Дата);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("Таблицапр_НачисленныеБаллы",
			пр_УправлениеБаллами.ПодготовитьТаблицуДвиженийДокументаНачисленныеБаллы(СтруктураШапкиДокумента));	
		
КонецПроцедуры

&Вместо("ПолучитьТекстЗапросаИнициализироватьДанные")
Функция пр_ПолучитьТекстЗапросаИнициализироватьДанные()
	
	Результат = ПродолжитьВызов();
	
	// Замена СчетаУчета из Номеклатуры на СчетЗатрат из табличной части
	СтрокаЗамены = "ВЫРАЗИТЬ (ВЫБОР КОГДА ПриходнаяНакладнаяРасходы.СчетЗатрат = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка) ТОГДА ПриходнаяНакладнаяРасходы.Номенклатура.СчетУчетаЗатрат ИНАЧЕ ПриходнаяНакладнаяРасходы.СчетЗатрат КОНЕЦ КАК ПланСчетов.Управленческий)";
	
	Результат = СтрЗаменить(Результат, "ПриходнаяНакладнаяРасходы.Номенклатура.СчетУчетаЗатрат", СтрокаЗамены); 
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 
	
#КонецЕсли 

