#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область Проведение

&После("СформироватьТаблицаРасчетыСПокупателями")
Процедура пр_СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства)
	
	пр_Период = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаРасходнаяНакладная, "Заказ", Истина);
	
	Если ЗначениеЗаполнено(пр_Период) Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРасчетыСПокупателями.ЗаполнитьЗначения(пр_Период, "Заказ");	
	КонецЕсли; 
	
КонецПроцедуры

&После("ИнициализироватьДанныеДокумента")
Процедура пр_ИнициализироватьДанныеДокумента(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства)
	
	пр_СформироватьТаблицуНачисленныеБаллы(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства);	
	пр_СформироватьТаблицупрНапоминанияОПродажах(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства);
	пр_СформироватьТаблицуфранПрограммныеПродуктыКонтрагентов(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства);
	
КонецПроцедуры

Процедура пр_СформироватьТаблицуНачисленныеБаллы(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(РасходнаяНакладная.Ответственный.Физлицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК Ответственный,
		|	РасходнаяНакладная.Дата КАК Дата
		|ИЗ
		|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
		|ГДЕ
		|	РасходнаяНакладная.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходнаяНакладная);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	СтруктураШапкиДокумента = Новый Структура;
	СтруктураШапкиДокумента.Вставить("Ссылка",			ДокументСсылкаРасходнаяНакладная);
	СтруктураШапкиДокумента.Вставить("Ответственный", 	Выборка.Ответственный);
	СтруктураШапкиДокумента.Вставить("Автор", 			Выборка.Ответственный);
	СтруктураШапкиДокумента.Вставить("Дата", 			Выборка.Дата);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("Таблицапр_НачисленныеБаллы",
			пр_УправлениеБаллами.ПодготовитьТаблицуДвиженийДокументаНачисленныеБаллы(СтруктураШапкиДокумента));	
		
КонецПроцедуры

Процедура пр_СформироватьТаблицупрНапоминанияОПродажах(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Запасы.Ссылка.Дата КАК ДатаПродажи,
		|	Запасы.Ссылка КАК ДокументПродажи,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Ссылка.Контрагент КАК Контрагент,
		|	Запасы.Ссылка.Договор КАК ДоговорКонтрагента,
		|	ДОБАВИТЬКДАТЕ(Запасы.Ссылка.Дата, ДЕНЬ, прПараметрыНапоминанийОПродажах.ДнейДействия) КАК ДатаНапоминания,
		|	СУММА(Запасы.Количество) КАК Количество
		|ИЗ
		|	Документ.РасходнаяНакладная.Запасы КАК Запасы
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.прПараметрыНапоминанийОПродажах КАК прПараметрыНапоминанийОПродажах
		|		ПО Запасы.Номенклатура = прПараметрыНапоминанийОПродажах.Номенклатура
		|ГДЕ
		|	Запасы.Ссылка = &Ссылка
		|	И прПараметрыНапоминанийОПродажах.ДнейДействия > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	Запасы.Ссылка,
		|	Запасы.Ссылка.Дата,
		|	Запасы.Номенклатура,
		|	Запасы.Ссылка.Контрагент,
		|	Запасы.Ссылка.Договор,
		|	ДОБАВИТЬКДАТЕ(Запасы.Ссылка.Дата, ДЕНЬ, прПараметрыНапоминанийОПродажах.ДнейДействия)";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходнаяНакладная);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицапрНапоминанияОПродажах", Запрос.Выполнить().Выгрузить());	
		
КонецПроцедуры

#Область ПрограммныеПродуктыКонтрагентов 

Процедура пр_СформироватьТаблицуфранПрограммныеПродуктыКонтрагентов(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос(ТекстЗапросаТаблицаПрограмныеПродукты());
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходнаяНакладная);
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаХарактеристик 	= РезультатыЗапроса[1].Выгрузить();
	ТаблицаДвижений 		= РезультатыЗапроса[2].Выгрузить();
	
	Если ТаблицаХарактеристик.Количество() > 0 Тогда
		
		ДополнитьТаблицуДвиженийДаннымиДополнительныхСвойств(ТаблицаДвижений, ТаблицаХарактеристик, СтруктураДополнительныеСвойства);
		
	КонецЕсли; 
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицафранПрограммныеПродуктыКонтрагентов", ТаблицаДвижений);	
		
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрограмныеПродукты()
	
	Текст = 
		"ВЫБРАТЬ
		|	Запасы.Ссылка.Контрагент КАК Контрагент,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК ХарактеристикаНоменклатуры,
		|	РасходнаяНакладнаяСерийныеНомера.СерийныйНомер КАК СерияНоменклатуры,
		|	ИСТИНА КАК Действует,
		|	"""" КАК Примечание,
		|	ЛОЖЬ КАК ОбслуживаетсяПоИТС,
		|	Запасы.Номенклатура.франБесплатнаяПодпискаИТС КАК ЛьготнаяПодписка,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Партнер,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Получатель,
		|	"""" КАК ИмяФайлаИзображения,
		|	Запасы.Ссылка.Дата КАК Период
		|ПОМЕСТИТЬ ПредварительнаяТаблица
		|ИЗ
		|	Документ.РасходнаяНакладная.Запасы КАК Запасы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходнаяНакладная.СерийныеНомера КАК РасходнаяНакладнаяСерийныеНомера
		|		ПО Запасы.ИдентификаторСтроки = РасходнаяНакладнаяСерийныеНомера.КлючСвязи
		|ГДЕ
		|	Запасы.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяСерийныеНомера.Ссылка = &Ссылка
		|	И Запасы.Номенклатура.франРегистрацияПродаж
		|	И РасходнаяНакладнаяСерийныеНомера.СерийныйНомер <> ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика
		|ИЗ
		|	Документ.РасходнаяНакладная.Запасы КАК РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	НЕ РасходнаяНакладнаяЗапасы.Характеристика.ПометкаУдаления
		|	И РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяЗапасы.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИСТИНА КАК Активность,
		|	ПредварительнаяТаблица.Период КАК Период,
		|	ПредварительнаяТаблица.Контрагент КАК Контрагент,
		|	ПредварительнаяТаблица.Номенклатура КАК Номенклатура,
		|	ПредварительнаяТаблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПредварительнаяТаблица.СерияНоменклатуры КАК СерияНоменклатуры,
		|	ПредварительнаяТаблица.Действует КАК Действует,
		|	ПредварительнаяТаблица.Примечание КАК Примечание,
		|	ПредварительнаяТаблица.ОбслуживаетсяПоИТС КАК ОбслуживаетсяПоИТС,
		|	ПредварительнаяТаблица.ЛьготнаяПодписка КАК ЛьготнаяПодписка,
		|	ПредварительнаяТаблица.Партнер КАК Партнер,
		|	ПредварительнаяТаблица.Получатель КАК Получатель,
		|	ПредварительнаяТаблица.ИмяФайлаИзображения КАК ИмяФайлаИзображения
		|ИЗ
		|	ПредварительнаяТаблица КАК ПредварительнаяТаблица";
	
	Возврат Текст;
		
КонецФункции

Процедура ЗаполнитьСвойстваПВХ(МассивСвойств, Свойства)
	
	МассивСвойств 	= Новый Массив;
	СвойстваПВХ 	= Новый Структура;
	
	СвойстваПВХ.Вставить("Апгрейд", 				"Апгрейд1eec4406-f609-4d30-bcb2-8a99c20087bf");
	СвойстваПВХ.Вставить("Апгрейд2", 				"Апгрейд 2dde3d8b0-ca61-469b-bb98-238140f88b68");
	СвойстваПВХ.Вставить("АпгрейдРегНомер", 		"Апгрейд - рег.номер173142e3-27a7-4557-bf81-089b6c324165");
	СвойстваПВХ.Вставить("Апгрейд2РегНомер", 		"Апгрейд 2 - рег.номерa03c2bcd-7df6-4654-b739-7d423c633418");
	СвойстваПВХ.Вставить("ДопЛицензия1", 			"Доп лицензия 1de884b8b-f666-49f8-a7c8-4d1a9d7e683b");
	СвойстваПВХ.Вставить("ДопЛицензия2", 			"Доп лицензия 219447219-45d5-44c0-8ddf-e526dea379a4");
	СвойстваПВХ.Вставить("ДопЛицензия3", 			"Доп лицензия 36c8efc21-0a63-499c-af42-6b9890dd1129");
	СвойстваПВХ.Вставить("ДопЛицензия1РегНомер", 	"Доп лицензия 1 - рег.номер3915ac9e-6899-4555-8da3-929fe72bb4e3");
	СвойстваПВХ.Вставить("ДопЛицензия2РегНомер", 	"Доп лицензия 2 - рег.номерb676504b-9b9e-4e95-a99c-831f8bebc30b");
	СвойстваПВХ.Вставить("ДопЛицензия3РегНомер", 	"Доп лицензия 3 - рег.номер8b9e7b25-5b01-4b2a-9279-e81e7eb8dea6");
	
	Для каждого ЭлементСтруктуры Из СвойстваПВХ Цикл
		
		Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", ЭлементСтруктуры.Значение);
	
		Если Не Свойство.Пустая() Тогда
			МассивСвойств.Добавить(Свойство);
			Свойства.Вставить(ЭлементСтруктуры.Ключ, Свойство);
		КонецЕсли; 
		
	КонецЦикла;
		
КонецПроцедуры

Функция НайтиСвойствоВТаблице(ИмяСвойства, Свойства, СтрокаТЧ, ТаблицаХарактеристик)
	
	ПараметрыОтбора = Новый Структура("ВладелецСвойств, Свойство", СтрокаТЧ.Характеристика, Свойства[ИмяСвойства]);
	Строки = ТаблицаХарактеристик.НайтиСтроки(ПараметрыОтбора);
	
	Если Строки.Количество() > 0 Тогда
		Значение = Строки[0].Значение;
	Иначе
		Значение = Неопределено;
	КонецЕсли; 
	
	Возврат Значение;
	
КонецФункции

Процедура НайтиДополнитьСтрокуДвиженийДополнительныхСвойств(СтрокаТЧ, Параметры)
	
	Фильтр = Новый Структура("Номенклатура, СерияНоменклатуры, ХарактеристикаНоменклатуры"); 
	
	Номенклатура = НайтиСвойствоВТаблице(Параметры.ИмяСвойства, Параметры.Свойства, СтрокаТЧ, Параметры.ЗначенияСвойствХарактеристик);
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		
		СерияНоменклатуры = НайтиСвойствоВТаблице(Параметры.ИмяСвойстваСерии, Параметры.Свойства, СтрокаТЧ, Параметры.ЗначенияСвойствХарактеристик);
		
		Фильтр.Номенклатура 				= Номенклатура;
		Фильтр.СерияНоменклатуры 			= СерияНоменклатуры;
		Фильтр.ХарактеристикаНоменклатуры 	= Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		
		Строки = Параметры.ТаблицаДвижений.НайтиСтроки(Фильтр);
		Если Строки.Количество() = 1 Тогда
			Строка = Строки[0];
		Иначе
			Строка = Параметры.ТаблицаДвижений.Добавить();
		КонецЕсли; 
		
		ЗаполнитьЗначенияСвойств(Строка, Параметры);
		Строка.Номенклатура			= Номенклатура;
		Строка.СерияНоменклатуры	= СерияНоменклатуры;
		
		Строка.Активность 			= Истина;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ДополнитьТаблицуДвиженийДаннымиДополнительныхСвойств(ТаблицаДвижений, ТаблицаХарактеристик, СтруктураДополнительныеСвойства)
	
	МассивСвойств 	= Новый Массив;
	Свойства 		= Новый Соответствие;
	ЗаполнитьСвойстваПВХ(МассивСвойств, Свойства);
		
	ЗначенияСвойствХарактеристик = УправлениеСвойствами.ЗначенияСвойств(
		ТаблицаХарактеристик.ВыгрузитьКолонку("Характеристика"), Истина, Ложь, МассивСвойств);
		
	Параметры = Новый Структура;
	Параметры.Вставить("ТаблицаДвижений", 				ТаблицаДвижений);
	Параметры.Вставить("ЗначенияСвойствХарактеристик", 	ЗначенияСвойствХарактеристик);
	Параметры.Вставить("Свойства", 						Свойства);
	Параметры.Вставить("Регистратор", 					СтруктураДополнительныеСвойства.СтруктураДанныхДокумента.Ссылка);
	Параметры.Вставить("Период", 						СтруктураДополнительныеСвойства.СтруктураДанныхДокумента.Дата);
	Параметры.Вставить("Контрагент", 					СтруктураДополнительныеСвойства.СтруктураДанныхДокумента.Контрагент);
	
	Для Каждого СтрокаТЧ Из ТаблицаХарактеристик Цикл
		
		// Апгрейд 1
		Параметры.Вставить("Действует", 		Ложь);
		Параметры.Вставить("ИмяСвойства", 		"Апгрейд");
		Параметры.Вставить("ИмяСвойстваСерии", 	"АпгрейдРегНомер");
		НайтиДополнитьСтрокуДвиженийДополнительныхСвойств(СтрокаТЧ, Параметры);
		
		// Апгрейд 2
		Параметры.Вставить("Действует", 		Ложь);
		Параметры.Вставить("ИмяСвойства", 		"Апгрейд2");
		Параметры.Вставить("ИмяСвойстваСерии", 	"Апгрейд2РегНомер");
		НайтиДополнитьСтрокуДвиженийДополнительныхСвойств(СтрокаТЧ, Параметры);
		
		// ДопЛицензия 1
		Параметры.Вставить("Действует", 		Истина);
		Параметры.Вставить("ИмяСвойства", 		"ДопЛицензия1");
		Параметры.Вставить("ИмяСвойстваСерии", 	"ДопЛицензия1РегНомер");
		НайтиДополнитьСтрокуДвиженийДополнительныхСвойств(СтрокаТЧ, Параметры);

		// ДопЛицензия 2
		Параметры.Вставить("Действует", 		Истина);
		Параметры.Вставить("ИмяСвойства", 		"ДопЛицензия2");
		Параметры.Вставить("ИмяСвойстваСерии", 	"ДопЛицензия2РегНомер");
		НайтиДополнитьСтрокуДвиженийДополнительныхСвойств(СтрокаТЧ, Параметры);
		
		// ДопЛицензия 3
		Параметры.Вставить("Действует", 		Истина);
		Параметры.Вставить("ИмяСвойства", 		"ДопЛицензия3");
		Параметры.Вставить("ИмяСвойстваСерии", 	"ДопЛицензия3РегНомер");
		НайтиДополнитьСтрокуДвиженийДополнительныхСвойств(СтрокаТЧ, Параметры);
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 
   	
#КонецЕсли  


