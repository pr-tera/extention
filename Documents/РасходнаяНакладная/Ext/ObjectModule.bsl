#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
     	
#Область ОбработчикиСобытий

&Перед("ПередЗаписью")
Процедура пр_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОтражатьВУправленческомУчете и НЕ ОтражатьВБухгалтерскомУчете Тогда
		Организация = Справочники.Организации.ОсновнаяОрганизация;	
	КонецЕсли; 
	
	Если НЕ УдалитьСделка.Пустая() Тогда
		
		Если НЕ ЗначениеЗаполнено(Заказ) Тогда
			Заказ = УдалитьСделка;
		КонецЕсли; 
		
		Для каждого СтрокаЗапасы Из Запасы Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаЗапасы.Заказ) Тогда
				СтрокаЗапасы.Заказ = УдалитьСделка;	
			КонецЕсли; 
			
		КонецЦикла;
		
		Для каждого СтрокаПредоплата Из Предоплата Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаПредоплата.Заказ) Тогда
				СтрокаПредоплата.Заказ = УдалитьСделка;	
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

&После("ПриЗаписи")
Процедура пр_ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли; 
	
	Если франСозданОбработкой Тогда
		
		НаборЗаписей = РегистрыСведений.франИсторияПакетногоФормирования.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Документ.Установить(Ссылка);

		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Документ 	= Ссылка;
		НоваяЗапись.Дата 	 	= Дата;
		НоваяЗапись.Организация	= Организация;
		НоваяЗапись.Контрагент	= Контрагент;
		
		НаборЗаписей.Записать();

	КонецЕсли; 
	
КонецПроцедуры

&Перед("ПриКопировании")
Процедура пр_ПриКопировании(ОбъектКопирования)
	
	ОтражатьВБухгалтерскомУчете 	= Ложь;	
	ОтражатьВУправленческомУчете 	= Ложь;
	
КонецПроцедуры

&Вместо("ОбработкаПроведения")
Процедура пр_ОбработкаПроведения(Отказ, РежимПроведения)
	
	// переопределение методов проведения документа для загруженных из УПП документов
	Если НЕ ОтражатьВУправленческомУчете И ОтражатьВБухгалтерскомУчете Тогда
		пр_ПереопределенноеПроведениеДокумента(Отказ, РежимПроведения);
	Иначе
		ПродолжитьВызов(Отказ, РежимПроведения);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

Процедура пр_ПереопределенноеПроведениеДокумента(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей.
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли 
