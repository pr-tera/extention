
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура пр_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.пр_ГруппаСобытия) ИЛИ Не Объект.Ссылка.Пустая() Тогда
		пр_КонтактПодтвержден = Истина;
	Иначе
		пр_КонтактПодтвержден = Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.пр_ГруппаСобытия, "ПодтверждениеКонтакта", Истина);
	КонецЕсли; 
	
	пр_УстановитьКонтрагентаДокумента();
	пр_ИзменениеРеквизитовФормы();
	пр_УстановитьПараметрыДинамическихСписков();
	пр_УстановитьУсловноеОформление();
	УстановитьКартинкуКнопки();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПриОткрытииПосле(Отказ)
	
	пр_ОбновитьИнформациюОДоговоре();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	
	// Запись формы документа "Обращение" 
	Если ИмяСобытия = "ИзмененоОбращение" И ЗначениеЗаполнено(Параметр) 
		И Не ЗначениеЗаполнено(Объект.пр_Обращение) Тогда
		
		Объект.пр_Обращение = Параметр;	
		пр_ОбновитьСвойстваКнопкиПривязатьОбращение();
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПередЗаписьюПеред(Отказ, ПараметрыЗаписи)
	
	Если Не пр_КонтактПодтвержден И ЗначениеЗаполнено(Объект.пр_ГруппаСобытия) 
		И пр_ПереходНаУНФВызовСервера.ЗначениеРеквизитаОбъекта(Объект.пр_ГруппаСобытия, "ПодтверждениеКонтакта", Истина) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Контакт не подтвержден.'"), Объект.Ссылка, "Объект.АбонентКонтакт", , Отказ); 
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура пр_АбонентПредставлениеПриИзмененииПосле(Элемент)
	
	пр_УстановитьКонтрагентаДокумента();
	пр_УстановитьПараметрыДинамическихСписков();
	пр_ОбновитьИнформациюОДоговоре();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_АбонентКонтактПриИзмененииПосле(Элемент)
	
	пр_УстановитьКонтрагентаДокумента();
	пр_УстановитьПараметрыДинамическихСписков();
	пр_ОбновитьИнформациюОДоговоре();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_сфпВладелецПриИзмененииПосле(Элемент)
	
	пр_УстановитьКонтрагентаДокумента();
	пр_УстановитьПараметрыДинамическихСписков();
	пр_ОбновитьИнформациюОДоговоре();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_Подключаемый_ИнформацияОДоговореНажатие(Элемент)
	
	Если НЕ пр_ДоговорКонтрагента.Пустая() Тогда
		
		ПоказатьЗначение(, пр_ДоговорКонтрагента); 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура пр_Подключаемый_ИнформацияОЧасахНажатие(Элемент)
	
	ПараметрыОткрытияФормы = Новый Структура;
	
	ПараметрыОткрытияФормы.Вставить("ПриОткрытииСформировать", 	Истина);
	ПараметрыОткрытияФормы.Вставить("Контрагент", 				пр_Контрагент);
	ПараметрыОткрытияФормы.Вставить("НачалоПериода", 			НачалоМесяца(ТекущаяДата()));
	ПараметрыОткрытияФормы.Вставить("КонецПериода", 			ТекущаяДата());
	
	ОткрытьФорму(
		"Отчет.пр_ОтчетПоПакетамЧасов.ФормаОбъекта", 
		ПараметрыОткрытияФормы, 
		ЭтаФорма, 
		УникальныйИдентификатор, , , , 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура пр_Подключаемый_ГруппаСобытияПриИзменении(Элемент)
	
	пр_ПриИзмененииГруппыСобытия();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура пр_ПривязатьОбращениеПосле(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.пр_Обращение) Тогда
		пр_РаботаСОбращениямиКлиент.ВвестиОбращение(ЭтотОбъект);
	Иначе
		
		ПараметрыОткрытияФормы = Новый Структура("Ключ", Объект.пр_Обращение);
		
		ОткрытьФорму(
			"Документ.прОбращение.ФормаОбъекта", 
			ПараметрыОткрытияФормы, 
			ЭтаФорма, 
			УникальныйИдентификатор, , , , 
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПривязатьОбращениеИзСписка(Команда)
	
	ТекущиеДанные = Элементы.пр_Обращения.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 	
		Возврат;
	КонецЕсли; 
	
	пр_РаботаСОбращениямиСервер.ПривязатьОбращениеНаСервере(ТекущиеДанные.Ссылка);
	
	Модифицированность = Истина;
	Объект.пр_ГруппаСобытия 	= пр_ГруппаСобытий1яЛиния();
	Объект.пр_Обращение 		= ТекущиеДанные.Ссылка;
	Объект.Тема					= "Повторное обращение. " + СокрЛП(Объект.Тема);
	
	пр_ОбновитьСвойстваКнопкиПривязатьОбращение();
	
	ТекущийЭлемент = Элементы.пр_ФормаОбращение;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_пр_ВыбораДоговораШагНазад(Команда)
	
	пр_ОтобразитьИнформациюПоСледующемуДоговору("Предыдущий");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_пр_ВыбораДоговораШагВперед(Команда)
	
	пр_ОтобразитьИнформациюПоСледующемуДоговору("Следующий");
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПодтвержденияКонтактаПосле(Команда)
	
	Если ЗначениеЗаполнено(Объект.АбонентКонтакт) И Не пр_КонтактПодтвержден Тогда
		
		пр_КонтактПодтвержден = Истина;
		УстановитьКартинкуКнопки();
		
		Модифицированность = Истина;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура пр_УстановитьУсловноеОформление()
	
	// Состояние Завершено
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "пр_Обращения.Состояние", Перечисления.СостоянияСобытий.Завершено);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "пр_Обращения");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветФона", WebЦвета.Роса);
	
	// Состояние CRM_Отложено или Отменено
	Состояния = Новый Массив;
	Состояния.Добавить(Перечисления.СостоянияСобытий.CRM_Отложено);
	Состояния.Добавить(Перечисления.СостоянияСобытий.Отменено);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "пр_Обращения.Состояние", Состояния, ВидСравненияКомпоновкиДанных.ВСписке);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "пр_Обращения");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветФона", WebЦвета.СеребристоСерый);
	
КонецПроцедуры

&НаСервере
Процедура пр_ИзменениеРеквизитовФормы()
	
	Элементы.ФормаПринятьОбращение.Заголовок 	= НСтр("ru='Зарегистрировать интерес'"); 
	Элементы.ФормаОтклонитьОбращение.Заголовок 	= НСтр("ru='Отклонить интерес'"); 
	
	пр_ОбновитьСвойстваКнопкиПривязатьОбращение();
	
	пр_ДобавитьГруппуЭлементовДоговорИТС();
	пр_ДобавитьГруппуЭлементовПростыеРешения();
	
КонецПроцедуры

&НаСервере
Процедура пр_ОбновитьСвойстваКнопкиПривязатьОбращение()
	
	Если ЗначениеЗаполнено(Объект.пр_Обращение) Тогда
		
		РеквизитыОбращения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.пр_Обращение, "Дата, Номер", Истина);
		
		Элементы.пр_ФормаОбращение.Заголовок = СтрШаблон(НСтр("ru='Обращение %1 от %2'"), 
			СокрЛП(ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыОбращения.Номер, Истина, Истина)), 
			Формат(РеквизитыОбращения.Дата, "ДФ=dd.MM.yy"));	
			
		Элементы.пр_ФормаОбращение.Картинка = БиблиотекаКартинок.УстановитьСвязь;	
			
	КонецЕсли; 
	
КонецПроцедуры

// Метод возвращает группу событий с наименованием "1-я линия (Повторная)"
//
// Параметры:
//  Нет 
//
// Возвращаемое значение:
//  СправочникСсылка.ГруппыСобытий
//          
&НаСервереБезКонтекста
Функция пр_ГруппаСобытий1яЛиния()
	
	Возврат Справочники.ГруппыСобытий.НайтиПоНаименованию("1-я линия (Повторная)");
	
КонецФункции

&НаСервере
Процедура пр_УстановитьПараметрыДинамическихСписков()

	СвязанныеКонтрагенты = Новый Массив;
	
	// Поиск возможности получить контрагента для отбора
	Если НЕ (Не ЗначениеЗаполнено(Объект.АбонентКонтакт) 
		ИЛИ ТипЗнч(Объект.АбонентКонтакт) = ТипЗнч("СправочникСсылка.СправочникСсылка.СтроковыеКонтактыВзаимодействий") 
		ИЛИ ТипЗнч(Объект.АбонентКонтакт) = ТипЗнч("СправочникСсылка.СправочникСсылка.Пользователи") 
		ИЛИ ТипЗнч(Объект.АбонентКонтакт) = ТипЗнч("СправочникСсылка.ФизическиеЛица")) Тогда
		
		Если Не ЗначениеЗаполнено(пр_Контрагент) Тогда
			пр_УстановитьКонтрагентаДокумента();	
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(пр_Контрагент) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПРСвязанныеКонтрагенты.СвязанныйКонтрагент КАК СвязанныйКонтрагент
			|ИЗ
			|	РегистрСведений.ПРСвязанныеКонтрагенты КАК ПРСвязанныеКонтрагенты
			|ГДЕ
			|	ПРСвязанныеКонтрагенты.Контрагент = &Контрагент
			|	И ПРСвязанныеКонтрагенты.ДатаОкончания >= &ДатаОкончания
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Контрагенты.Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.Ссылка = &Контрагент";
			
			Запрос.УстановитьПараметр("ДатаОкончания", 	ТекущаяДатаСеанса());
			Запрос.УстановитьПараметр("Контрагент", 	пр_Контрагент);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			СвязанныеКонтрагенты = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("СвязанныйКонтрагент");
			
		КонецЕсли; 

	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(пр_Обращения, "Контрагент", 
		СвязанныеКонтрагенты, , , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный); 
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(пр_Обращения, "ПометкаУдаления", 
		Ложь, , , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный); 
		
КонецПроцедуры

&НаСервере
Процедура пр_УстановитьКонтрагентаДокумента()
	
	Если ЗначениеЗаполнено(сфпВладелец) Тогда
		
		пр_Контрагент = сфпВладелец;	
			
	ИначеЕсли ТипЗнч(Объект.АбонентКонтакт) = Тип("СправочникСсылка.Контрагенты") Тогда 
		
		пр_Контрагент = Объект.АбонентКонтакт;	
		
	ИначеЕсли ТипЗнч(Объект.АбонентКонтакт) = Тип("СправочникСсылка.CRM_ПотенциальныеКлиенты") Тогда 
		
		пр_Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.АбонентКонтакт, "Партнер", Истина);
		
	ИначеЕсли ТипЗнч(Объект.АбонентКонтакт) = Тип("СправочникСсылка.КонтактныеЛица") Тогда 
		
		пр_Контрагент = CRM_КлиентыСервер.НайтиПартнераПоКонтактномуЛицу(Объект.АбонентКонтакт);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура пр_ДобавитьГруппуЭлементовПростыеРешения()
	
	// Корневая группа
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.ГруппаДатаВремяЗвонка, Элементы.ДекорацияДатаЗвонка);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
 	пр_ГруппаПростыеРешения = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "пр_ГруппаЭлементыПростыеРешения");
	
	КонтекстЭлемента = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаЭлементыПростыеРешения);
	ЭлементГруппаСобытия = РедакторФорм.ДобавитьРеквизитОбъектаНаФорму(КонтекстЭлемента, "пр_ГруппаСобытия");
	
	ЭлементГруппаСобытия.АвтоМаксимальнаяШирина = Ложь;
	ЭлементГруппаСобытия.МаксимальнаяШирина 	= 12;
	ЭлементГруппаСобытия.Ширина 				= 12;
	ЭлементГруппаСобытия.Заголовок 				= "Группа";
	
	ЭлементГруппаСобытия.УстановитьДействие("ПриИзменении", "пр_Подключаемый_ГруппаСобытияПриИзменении");
	
КонецПроцедуры

#Область ГруппаОбъектовДоговорИТС

&НаСервере
Процедура пр_ДобавитьГруппуЭлементовДоговорИТС()
	
	// Корневая группа
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, ЭтаФорма, Элементы.ДекорацияРазделительКомментарий);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
 	пр_ГруппаДоговорИТС = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "пр_ГруппаДоговорИТС");

	
	// Группа кнопок переключения договоров лево/право.
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаДоговорИТС);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
 	пр_ГруппаСтрелкиВыбораДоговора = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "ГруппаСтрелкиВыбораДоговора");
	
	// Лево
	КонтекстКоманды = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаСтрелкиВыбораДоговора);	
	пр_ВыбораДоговораШагНазад = РедакторФорм.ДобавитьКомандуИКнопкуНаФорму(КонтекстКоманды, "пр_ВыбораДоговораШагНазад", "Шаг назад");
	пр_ВыбораДоговораШагНазад.Картинка 		= БиблиотекаКартинок.ПРШагНазад;
	пр_ВыбораДоговораШагНазад.Отображение 	= ОтображениеКнопки.Картинка;
	
	// Право
	КонтекстКоманды = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаСтрелкиВыбораДоговора);	
	пр_ВыбораДоговораШагВперед = РедакторФорм.ДобавитьКомандуИКнопкуНаФорму(КонтекстКоманды, "пр_ВыбораДоговораШагВперед", "Шаг назад");
	пр_ВыбораДоговораШагВперед.Картинка 	= БиблиотекаКартинок.пр_ШагВперед;
	пр_ВыбораДоговораШагВперед.Отображение 	= ОтображениеКнопки.Картинка;
	
	// Группа декораций линий, фреш, информация по договору.
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаДоговорИТС);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
 	пр_ГруппаОписаниеДоговора = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "ГруппаОписаниеДоговора");
	
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	пр_ДекорацияЛиния1Есть = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстДекорации, "ДекорацияЛиния1Есть");
	пр_ДекорацияЛиния1Есть.Картинка = БиблиотекаКартинок.пр_Линия1Есть;
	
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	пр_ДекорацияЛиния1Нет = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстДекорации, "ДекорацияЛиния1Нет");
	пр_ДекорацияЛиния1Нет.Картинка = БиблиотекаКартинок.пр_Линия1Нет;
	
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	пр_ДекорацияЛиния2Есть = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстДекорации, "ДекорацияЛиния2Есть");
	пр_ДекорацияЛиния2Есть.Картинка = БиблиотекаКартинок.пр_Линия2Есть;
	
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	пр_ДекорацияЛиния2Нет = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстДекорации, "ДекорацияЛиния2Нет");
	пр_ДекорацияЛиния2Нет.Картинка = БиблиотекаКартинок.пр_Линия2Нет;
	
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	пр_ДекорацияФреш = РедакторФорм.ДобавитьДекорациюКартинкуНаФорму(КонтекстДекорации, "ДекорацияФреш");
	пр_ДекорацияФреш.Картинка = БиблиотекаКартинок.пр_Фреш;
	
	СвойстваДекорации = Новый Структура;
	СвойстваДекорации.Вставить("Заголовок", 				"Информация о договоре");
	СвойстваДекорации.Вставить("Гиперссылка", 				Истина);
	СвойстваДекорации.Вставить("РастягиватьПоГоризонтали", 	Истина);
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора, , СвойстваДекорации);	
	пр_ИнформацияОДоговоре = РедакторФорм.ДобавитьДекорациюНадписьНаФорму(КонтекстДекорации, "ИнформацияОДоговоре");
	пр_ИнформацияОДоговоре.УстановитьДействие("Нажатие", "пр_Подключаемый_ИнформацияОДоговореНажатие");
	
	КонтекстПоля = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора);	
	КонтекстПоля.Свойства.Вставить("ПутьКДанным", 	"пр_Контрагент");
	КонтекстПоля.Свойства.Вставить("Видимость", 	Ложь);
	ПолеФормы = РедакторФорм.ДобавитьПолеНаФорму(КонтекстПоля, "ДоговорКонтрагента");
	
	// Группа декораций информация о часах
	КонтекстГруппы = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, Элементы.пр_ГруппаДоговорИТС);
	КонтекстГруппы.Свойства.Вставить("ОтображатьЗаголовок", 	Ложь);
	КонтекстГруппы.Свойства.Вставить("Группировка", 			ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
 	пр_ГруппаЧасы = РедакторФорм.ДобавитьГруппуОбычную(КонтекстГруппы, "ГруппаЧасы");
	
	СвойстваДекорации = Новый Структура;
	СвойстваДекорации.Вставить("Заголовок", 	"Информация о часах");
	СвойстваДекорации.Вставить("Гиперссылка", 	Истина);
	КонтекстДекорации = РедакторФорм.СоздатьКонтекстЭлемента(ЭтаФорма, пр_ГруппаОписаниеДоговора, , СвойстваДекорации);	
	пр_ИнформацияОЧасах = РедакторФорм.ДобавитьДекорациюНадписьНаФорму(КонтекстДекорации, "ИнформацияОЧасах");
	пр_ИнформацияОЧасах.УстановитьДействие("Нажатие", "пр_Подключаемый_ИнформацияОЧасахНажатие");

КонецПроцедуры

// Метод изменяет реквизит формы "пр_ДоговорКонтрагента", перемещая текущий договор
// в рамках таблицы договоров "пр_СписокДоговоров". 
//
// Параметры:
//  Нет
//    
&НаКлиенте
Процедура пр_ОтобразитьИнформациюПоСледующемуДоговору(Направление)
	
	ПараметрыОтбора = Новый Структура("ДоговорКонтрагента", пр_ДоговорКонтрагента);
	ТекущаяСтрокаВТаблице = пр_СписокДоговоров.НайтиСтроки(ПараметрыОтбора);  
	
	Если ТекущаяСтрокаВТаблице.Количество() = 0 Тогда
		ВызватьИсключение(НСтр("ru='Ошибка поиска позиции текущего договора.'"));	
	КонецЕсли; 
	
	ТекущийИдентификатор = ТекущаяСтрокаВТаблице[0].ПолучитьИдентификатор();
	
	Если Направление = "Предыдущий" Тогда
		НовыйИдентификатор = Макс(0, ТекущийИдентификатор - 1); 
	Иначе  		
		НовыйИдентификатор = Мин(пр_СписокДоговоров.Количество() - 1, ТекущийИдентификатор + 1); 
	КонецЕсли; 
	
	Если пр_ДоговорКонтрагента <> пр_СписокДоговоров[НовыйИдентификатор].ДоговорКонтрагента Тогда
		
		пр_ДоговорКонтрагента = пр_СписокДоговоров[НовыйИдентификатор].ДоговорКонтрагента; 
		пр_УправлениеДекорациямиДоговора();	
		
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура пр_УправлениеДекорациямиДоговора()
	
	ПараметрыОбъекта = пр_ПереходНаУНФКлиент.ПараметрыОбъектаФормыДляУправленияДекорациями();
	
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Контрагент 		= пр_Контрагент;
	ПараметрыОбъекта.ДоговорКонтрагента = пр_ДоговорКонтрагента;
	ПараметрыОбъекта.ВидОбращения 		= Объект.пр_ВидОбращения;
	
	пр_ПереходНаУНФКлиент.УправлениеДекорациямиДоговора(ЭтотОбъект, ПараметрыОбъекта); 
	
КонецПроцедуры

// Управление декорациями и изображениями по договорам контрагента
//
// Параметры:
//  Нет.
//
&НаКлиенте 
Процедура пр_ОбновитьИнформациюОДоговоре()
	
	Если НЕ пр_Контрагент.Пустая() Тогда
		пр_ПолучитьСписокДоговоровКонтрагента();
	Иначе
		пр_СписокДоговоров.Очистить();
	КонецЕсли; 
	
	Элементы.ГруппаСтрелкиВыбораДоговора.Видимость = пр_СписокДоговоров.Количество() > 1;
	
	пр_УправлениеДекорациямиДоговора();
	
КонецПроцедуры

&НаСервере
Процедура пр_ПолучитьСписокДоговоровКонтрагента()
	
	пр_СписокДоговоров.Загрузить(пр_Служебные.ПолучитьСписокДоговоров(пр_Контрагент, Объект.Дата, Истина));
	
	Если пр_СписокДоговоров.Количество() > 0 Тогда
		пр_ДоговорКонтрагента = пр_СписокДоговоров[0].ДоговорКонтрагента;
	Иначе
		пр_ДоговорКонтрагента = "";
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

&НаСервере
Процедура пр_ПриИзмененииГруппыСобытия()
	
	Если Не ЗначениеЗаполнено(Объект.пр_ГруппаСобытия) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыГруппыСобытий = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.пр_ГруппаСобытия, 
		"ОписаниеСобытия, ПодтверждениеКонтакта", Истина);
	
	Если ЗначениеЗаполнено(СокрЛП(РеквизитыГруппыСобытий.ОписаниеСобытия)) И 
		НЕ ЗначениеЗаполнено(СокрЛП(Объект.Комментарий)) Тогда
		
		Объект.Комментарий = РеквизитыГруппыСобытий.ОписаниеСобытия;
		
	КонецЕсли;
	
	пр_КонтактПодтвержден = Не РеквизитыГруппыСобытий.ПодтверждениеКонтакта;	
	
	УстановитьКартинкуКнопки();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКартинкуКнопки()
	
	Элементы.пр_ПодтвержденияКонтакта.Картинка = ?(пр_КонтактПодтвержден,
		БиблиотекаКартинок.Успешно, 
		БиблиотекаКартинок.Минус);
	
КонецПроцедуры

#КонецОбласти





