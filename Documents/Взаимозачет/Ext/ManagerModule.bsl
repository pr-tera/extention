#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область Проведение

&После("ИнициализироватьДанныеДокумента")
Процедура пр_ИнициализироватьДанныеДокумента(ДокументСсылкаВзаимозачет, СтруктураДополнительныеСвойства)
	
	Для каждого ЭлементСтруктуры Из СтруктураДополнительныеСвойства.ТаблицыДляДвижений Цикл
		
		ТаблицаДляДвижения = ЭлементСтруктуры.Значение;
		
		Если ТаблицаДляДвижения.Количество() = 1 Тогда
			Продолжить;	
		КонецЕсли; 
		
		Если ТаблицаДляДвижения.Колонки.Найти("Контрагент") <> Неопределено 
			И ТаблицаДляДвижения.Колонки.Найти("Договор") <> Неопределено Тогда
			
			МассивДоговоров = ТаблицаДляДвижения.ВыгрузитьКолонку("Договор");
			ДоговораИИхВладельцы = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивДоговоров, "Владелец", Истина);
			
			Для каждого СтрокаТаблицыДвижений Из ТаблицаДляДвижения Цикл
				
				ВладелецДоговора = ДоговораИИхВладельцы[СтрокаТаблицыДвижений.Договор].Владелец;
				
				Если СтрокаТаблицыДвижений.Контрагент <> ВладелецДоговора Тогда
					СтрокаТаблицыДвижений.Контрагент = ВладелецДоговора;	
				КонецЕсли; 
				
			КонецЦикла; 
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
