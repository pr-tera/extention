#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

&После("ПриЗаписи")
Процедура пр_ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли; 
	
	Если Отказ Тогда
		Возврат;	
	КонецЕсли; 
	
	ИнициализироватьДвиженияНачисленныеБаллы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДвиженияНачисленныеБаллы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Событие.Дата КАК Дата,
		|	Событие.Состояние КАК Состояние,
		|	ЕСТЬNULL(Событие.Автор.ФизическоеЛицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК Автор,
		|	ЕСТЬNULL(Событие.Ответственный.Физлицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК Ответственный,
		|	Событие.ВходящееИсходящееСобытие КАК ТипСобытия,
		|	ВЫБОР
		|		КОГДА Событие.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытий.ТелефонныйЗвонок)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСобытий.ТелефонныйЗвонок)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСобытий.ПустаяСсылка)
		|	КОНЕЦ КАК ВидСобытия,
		|	Событие.ГруппаСобытия КАК ГруппаСобытия
		|ИЗ
		|	Документ.Событие КАК Событие
		|ГДЕ
		|	Событие.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	СтруктураШапкиДокумента = Новый Структура;
	СтруктураШапкиДокумента.Вставить("Ссылка",				Ссылка);
	СтруктураШапкиДокумента.Вставить("Дата", 				Выборка.Дата);
	СтруктураШапкиДокумента.Вставить("Состояние",			Выборка.Состояние);
	СтруктураШапкиДокумента.Вставить("КоличествоЗвонков",	1);
	
	СтруктураШапкиДокумента.Вставить("Ответственный", 		Выборка.Ответственный);
	СтруктураШапкиДокумента.Вставить("Автор", 				Выборка.Ответственный);
	
	СтруктураШапкиДокумента.Вставить("ТипСобытия", 			Выборка.ТипСобытия);
	СтруктураШапкиДокумента.Вставить("ВидСобытия", 			Выборка.ВидСобытия);
	СтруктураШапкиДокумента.Вставить("ЕстьВложения", 		Ложь);
	СтруктураШапкиДокумента.Вставить("ГруппаСобытия", 		Выборка.ГруппаСобытия);
	
	ТаблицаДвижений = пр_УправлениеБаллами.ПодготовитьТаблицуДвиженийДокументаНачисленныеБаллы(СтруктураШапкиДокумента);	
	
	Если ТаблицаДвижений.Количество() > 0 Тогда
		
		ТаблицаДвижений.Колонки.Добавить("Регистратор");

		ТаблицаДвижений.ЗаполнитьЗначения(Ссылка, 				"Регистратор");
		ТаблицаДвижений.ЗаполнитьЗначения(НЕ ПометкаУдаления, 	"Активность");
		
		НаборЗаписей = РегистрыНакопления.пр_НачисленныеБаллы.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Ссылка);
		НаборЗаписей.Загрузить(ТаблицаДвижений);
		
		НаборЗаписей.Записать();
		
	КонецЕсли; 
			
КонецПроцедуры

#КонецОбласти	
	
#КонецЕсли 



