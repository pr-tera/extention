#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
     	
#Область СлужебныеПроцедурыИФункции

&ИзменениеИКонтроль("ИнициализироватьДанныеДокумента")
Процедура пр_ИнициализироватьДанныеДокумента(ДокументСсылкаПриемНаРаботу, СтруктураДополнительныеСвойства)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ 
	|	&Организация КАК Организация,
	|	ПриемНаРаботуСотрудники.НомерСтроки,
	|	ПриемНаРаботуСотрудники.Сотрудник,
	|	ПриемНаРаботуСотрудники.СтруктурнаяЕдиница,
	|	ПриемНаРаботуСотрудники.Должность,
	|	ПриемНаРаботуСотрудники.ГрафикРаботы,
	|	ПриемНаРаботуСотрудники.ЗанимаемыхСтавок, 
	#Вставка 
	|	ПриемНаРаботуСотрудники.пр_Грейд КАК пр_Грейд,
	#КонецВставки
	|	ПриемНаРаботуСотрудники.Период
	|ПОМЕСТИТЬ ТаблицаСотрудники
	|ИЗ
	|	Документ.ПриемНаРаботу.Сотрудники КАК ПриемНаРаботуСотрудники
	|ГДЕ
	|	ПриемНаРаботуСотрудники.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	&Организация КАК Организация,
	|	ПриемНаРаботуСотрудники.НомерСтроки,
	|	ПриемНаРаботуСотрудники.Сотрудник,
	|	ПриемНаРаботуСотрудники.Период,
	|	ПриемНаРаботуНачисленияУдержания.ВидНачисленияУдержания,
	|	ПриемНаРаботуНачисленияУдержания.Валюта,
	|	ПриемНаРаботуНачисленияУдержания.Сумма,
	|	ПриемНаРаботуНачисленияУдержания.СчетЗатрат
	|ПОМЕСТИТЬ ТаблицаНачисленияУдержания
	|ИЗ
	|	Документ.ПриемНаРаботу.Сотрудники КАК ПриемНаРаботуСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботу.НачисленияУдержания КАК ПриемНаРаботуНачисленияУдержания
	|		ПО ПриемНаРаботуСотрудники.КлючСвязи = ПриемНаРаботуНачисленияУдержания.КлючСвязи
	|			И ПриемНаРаботуСотрудники.Ссылка = ПриемНаРаботуНачисленияУдержания.Ссылка
	|ГДЕ
	|	ПриемНаРаботуСотрудники.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	ПриемНаРаботуСотрудники.НомерСтроки,
	|	ПриемНаРаботуСотрудники.Сотрудник,
	|	ПриемНаРаботуСотрудники.Период,
	|	ПриемНаРаботуНалогиНаДоходы.ВидНачисленияУдержания,
	|	ПриемНаРаботуНалогиНаДоходы.Валюта,
	|	0,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	Документ.ПриемНаРаботу.Сотрудники КАК ПриемНаРаботуСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботу.НалогиНаДоходы КАК ПриемНаРаботуНалогиНаДоходы
	|		ПО ПриемНаРаботуСотрудники.КлючСвязи = ПриемНаРаботуНалогиНаДоходы.КлючСвязи
	|			И ПриемНаРаботуСотрудники.Ссылка = ПриемНаРаботуНалогиНаДоходы.Ссылка
	|ГДЕ
	|	ПриемНаРаботуСотрудники.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ТаблицаСотрудники.Организация,
	|	ТаблицаСотрудники.НомерСтроки,
	|	ТаблицаСотрудники.Сотрудник,
	|	ТаблицаСотрудники.СтруктурнаяЕдиница,
	|	ТаблицаСотрудники.Должность,
	|	ТаблицаСотрудники.ГрафикРаботы,
	|	ТаблицаСотрудники.ЗанимаемыхСтавок,
	#Вставка 
	|	ТаблицаСотрудники.пр_Грейд КАК пр_Грейд,
	#КонецВставки
	|	ТаблицаСотрудники.Период
	|ИЗ
	|	ТаблицаСотрудники КАК ТаблицаСотрудники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ТаблицаНачисленияУдержания.Организация,
	|	ТаблицаНачисленияУдержания.НомерСтроки,
	|	ТаблицаНачисленияУдержания.Сотрудник,
	|	ТаблицаНачисленияУдержания.Период,
	|	ТаблицаНачисленияУдержания.ВидНачисленияУдержания,
	|	ТаблицаНачисленияУдержания.Валюта,
	|	ТаблицаНачисленияУдержания.Сумма,
	|	ТаблицаНачисленияУдержания.СчетЗатрат,
	|	ИСТИНА КАК Актуальность
	|ИЗ
	|	ТаблицаНачисленияУдержания КАК ТаблицаНачисленияУдержания");

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриемНаРаботу);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	МассивРезультатов = Запрос.ВыполнитьПакет();

	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСотрудники", 				МассивРезультатов[2].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПлановыеНачисленияИУдержания", МассивРезультатов[3].Выгрузить());
	#Вставка 
		пр_ЗаполнитьТаблицуПоказателиKPI(ДокументСсылкаПриемНаРаботу, СтруктураДополнительныеСвойства);
	#КонецВставки

	СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;

КонецПроцедуры

Процедура пр_ЗаполнитьТаблицуПоказателиKPI(ДокументСсылкаПриемНаРаботу, СтруктураДополнительныеСвойства)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриемНаРаботуСотрудники.Период КАК Период,
		|	ПриемНаРаботуСотрудники.Сотрудник КАК Сотрудник,
		|	ПриемНаРаботуСотрудники.Сотрудник.Физлицо КАК ФизическоеЛицо,
		|	ПриемНаРаботуСотрудники.пр_НормаЧасовМинимум КАК НормаЧасовМинимум,
		|	ПриемНаРаботуСотрудники.пр_НормаЧасовМаксимум КАК НормаЧасовМаксимум,
		|	ПриемНаРаботуСотрудники.пр_Оклад КАК Оклад,
		|	ПриемНаРаботуСотрудники.пр_ПремияЗаРезультат КАК ПремияЗаРезультат,
		|	ПриемНаРаботуСотрудники.пр_Премия4Плюс КАК Премия4Плюс,
		|	ПриемНаРаботуСотрудники.пр_КонтролироватьЗаполнениеОтчета КАК КонтролироватьЗаполнениеОтчета,
		|	ПриемНаРаботуСотрудники.пр_КоэффициентРабочегоВремениУменьшенный КАК КоэффициентРабочегоВремениУменьшенный,
		|	ПриемНаРаботуСотрудники.пр_КоэффициентРабочегоВремени КАК КоэффициентРабочегоВремени,
		|	ПриемНаРаботуСотрудники.пр_КоэффициентРабочегоВремениУвеличенный КАК КоэффициентРабочегоВремениУвеличенный,
		|	ИСТИНА КАК РазрешитьПоискИсполнителя,
		|	ПриемНаРаботуСотрудники.пр_Руководитель КАК Руководитель,
		|	ПриемНаРаботуСотрудники.пр_НеЗаполнятьТипЗадания КАК НеЗаполнятьТипЗадания
		|ИЗ
		|	Документ.ПриемНаРаботу.Сотрудники КАК ПриемНаРаботуСотрудники
		|ГДЕ
		|	ПриемНаРаботуСотрудники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриемНаРаботу);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПоказателиKPI", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли 

