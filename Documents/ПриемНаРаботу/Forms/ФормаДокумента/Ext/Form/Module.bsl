#Область ОбработчикиСобытийФормы

&НаСервере
Процедура пр_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	пр_ИзменениеРеквизитовФормы();
	пр_ВосстановитьНастройкуОтображенияДопРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПриОткрытииПосле(Отказ)
	
	пр_РаботаСФормамиКлиент.УправлениеВидимостьюДополнительныхЭлементовФормыКадровогоДокумена(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПриЗакрытииПосле(ЗавершениеРаботы)
	
	пр_СохранитьНастройкуОтображенияДопРеквизитов(пр_ОтображатьДополнительныеКолонки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура пр_ЗаполнитьНачисленияПослеПосле(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("пр_ЗаполнениеНачисленийУдержаний", ЭтотОбъект);
	
	Если Объект.НачисленияУдержания.Количество() > 0 Тогда
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='При заполнении все текущие начисления будут очищены. Продолжить?'"), 
			РежимДиалогаВопрос.ДаНетОтмена, 60);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ПоказатьСкрытьДополнительныеРеквизитыПосле(Команда)
	
	пр_ОтображатьДополнительныеКолонки = Не пр_ОтображатьДополнительныеКолонки;
	пр_РаботаСФормамиКлиент.УправлениеВидимостьюДополнительныхЭлементовФормыКадровогоДокумена(ЭтаФорма);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура пр_ИзменениеРеквизитовФормы()
	
	пр_РаботаСФормами.ДобавитьРеквизитыТЧСотрудникиКадровыхДокументов(ЭтаФорма, Элементы.Сотрудники, Элементы.СотрудникиТН);
	
КонецПроцедуры

&НаКлиенте
Процедура пр_ЗаполнениеНачисленийУдержаний(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		пр_ЗаполнитьНачисленияПослеПослеНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура пр_ЗаполнитьНачисленияПослеПослеНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.пр_ЗаполнитьНачисленияПоГрейдам();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура пр_СохранитьНастройкуОтображенияДопРеквизитов(Настройка)
	
	ХранилищеОбщихНастроек.Сохранить("Документ.ПриемНаРаботу", "ОтображатьДополнительныеКолонки", Настройка);

КонецПроцедуры

&НаСервере
Процедура пр_ВосстановитьНастройкуОтображенияДопРеквизитов()
	
	пр_ОтображатьДополнительныеКолонки = ХранилищеОбщихНастроек.Загрузить("Документ.ПриемНаРаботу", "ОтображатьДополнительныеКолонки");

КонецПроцедуры

#КонецОбласти
